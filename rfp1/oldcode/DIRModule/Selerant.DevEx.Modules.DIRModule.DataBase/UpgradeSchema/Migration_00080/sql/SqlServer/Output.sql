IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_OUTPUT$InsertRecord'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_OUTPUT$InsertRecord]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_OUTPUT$InsertRecord]
	@dcID BIGINT,
	@sASSESSMENT_CODE VARCHAR(32),
	@dcOUTPUT_CATEGORY_ID BIGINT,
	@sDESTINATION_CODE VARCHAR(64),
	@dcASSESSMENT_LC_STAGE_ID BIGINT,
	@dcINPUT_ID BIGINT,
	@sMAT_PLANT VARCHAR(20),
	@sMAT_CODE VARCHAR(32),
	@dcOUTPUT_COST NUMERIC(24,4),
	@dcCOST NUMERIC(24,4),
	@dcINCOME NUMERIC(24,4),
	@dcWEIGHT NUMERIC(19,3),
	@dcSORT_ORDER INT
AS
BEGIN
	INSERT [dbo].[DXDIR_OUTPUT]
	(
		[ID],
		[ASSESSMENT_CODE],
		[OUTPUT_CATEGORY_ID],
		[DESTINATION_CODE],
		[ASSESSMENT_LC_STAGE_ID],
		[INPUT_ID],
		[MAT_PLANT],
		[MAT_CODE],
		[OUTPUT_COST],
		[COST],
		[INCOME],
		[WEIGHT],
		[SORT_ORDER]
	)
	VALUES
	(
		@dcID,
		@sASSESSMENT_CODE,
		@dcOUTPUT_CATEGORY_ID,
		@sDESTINATION_CODE,
		@dcASSESSMENT_LC_STAGE_ID,
		@dcINPUT_ID,
		@sMAT_PLANT,
		@sMAT_CODE,
		@dcOUTPUT_COST,
		@dcCOST,
		@dcINCOME,
		@dcWEIGHT,
		@dcSORT_ORDER
	)
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_OUTPUT$UpdateRecord'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_OUTPUT$UpdateRecord]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_OUTPUT$UpdateRecord]
	@dcID BIGINT,
	@sASSESSMENT_CODE VARCHAR(32),
	@dcOUTPUT_CATEGORY_ID BIGINT,
	@sDESTINATION_CODE VARCHAR(64),
	@dcASSESSMENT_LC_STAGE_ID BIGINT,
	@dcINPUT_ID BIGINT,
	@sMAT_PLANT VARCHAR(20),
	@sMAT_CODE VARCHAR(32),
	@dcOUTPUT_COST NUMERIC(24,4),
	@dcCOST NUMERIC(24,4),
	@dcINCOME NUMERIC(24,4),
	@dcWEIGHT NUMERIC(19,3),
	@dcSORT_ORDER INT
AS
BEGIN
	UPDATE [dbo].[DXDIR_OUTPUT]
	SET 
		[ASSESSMENT_CODE] = @sASSESSMENT_CODE,
		[OUTPUT_CATEGORY_ID] = @dcOUTPUT_CATEGORY_ID,
		[DESTINATION_CODE] = @sDESTINATION_CODE,
		[ASSESSMENT_LC_STAGE_ID] = @dcASSESSMENT_LC_STAGE_ID,
		[INPUT_ID] = @dcINPUT_ID,
		[MAT_PLANT] = @sMAT_PLANT,
		[MAT_CODE] = @sMAT_CODE,
		[OUTPUT_COST] = @dcOUTPUT_COST,
		[COST] = @dcCOST,
		[INCOME] = @dcINCOME,
		[WEIGHT] = @dcWEIGHT,
		[SORT_ORDER] = @dcSORT_ORDER
	WHERE [ID] = @dcID
	
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_OUTPUT$DeleteRecord'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_OUTPUT$DeleteRecord]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_OUTPUT$DeleteRecord]
	@dcID BIGINT
AS
BEGIN
	DELETE FROM [dbo].[DXDIR_OUTPUT]
	WHERE [ID] = @dcID
END
GO

IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_OUTPUT$GetNextId'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_OUTPUT$GetNextId]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_OUTPUT$GetNextId]
	@dcNextId BIGINT OUTPUT 
AS
BEGIN
	SET @dcNextId = NULL

	DECLARE @dbname NVARCHAR(128) = db_name()

	EXEC DX_PK_UTILITY_SQLSRV$db_sp_get_next_sequence_value @dbname
		,N'dbo'
		,N'DXDIR_OUTPUT_SEQ'
		,@dcNextId OUTPUT
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_OUTPUT$InsertOrUpdateNonWaste'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_OUTPUT$InsertOrUpdateNonWaste]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_OUTPUT$InsertOrUpdateNonWaste]
	@sASSESSMENT_CODE VARCHAR(32),
    @sDESTINATION_CODE VARCHAR(32),
    @dcASSESSMENT_LC_STAGE_ID BIGINT
AS
BEGIN
	DECLARE @outputCategoryId BIGINT,
			@outputId BIGINT,
			@destinationSortOrder INT;

    -- OUTPUT_CATEGORY_ID (1, 2 or 3)
    SET @outputCategoryId = CASE
								WHEN @sDESTINATION_CODE LIKE 'PRODUCT%'
									THEN 1
								WHEN @sDESTINATION_CODE LIKE 'COPRODUCT%'
									THEN 2
								WHEN @sDESTINATION_CODE = 'FOOD_RESCUE'
									THEN 3
								ELSE
									NULL
							END;

	IF (@outputCategoryId IS NOT NULL)
	BEGIN
		
		IF NOT EXISTS (SELECT * FROM DXDIR_OUTPUT
					   WHERE ASSESSMENT_CODE = @sASSESSMENT_CODE AND
						     ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND
						     OUTPUT_CATEGORY_ID = @outputCategoryId AND 
							 DESTINATION_CODE = @sDESTINATION_CODE)
		BEGIN
			EXEC dbo.DXDIR_PK_OUTPUT$GetNextId @outputId OUTPUT;
		END

		SELECT @destinationSortOrder = SORT_ORDER
		FROM DXDIR_DESTINATION
		WHERE CODE = @sDESTINATION_CODE;
       
		-- sum values and update/insert targer        
		MERGE INTO DXDIR_OUTPUT T
		USING (SELECT SUM((DXDIR_INPUT_DESTINATION.PERCENTAGE / 100.0) * DXDIR_INPUT.COST *
						(CASE
							WHEN DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
							THEN 1 - DXDIR_INPUT.INEDIBLE_PARTS
							ELSE
								1
						END)) as COST_SUM,
					SUM((DXDIR_INPUT_DESTINATION.PERCENTAGE / 100.0) * DXDIR_INPUT.MASS *
						(CASE
							WHEN DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
							THEN 1 - DXDIR_INPUT.INEDIBLE_PARTS
							ELSE
								1
						END)) as MASS_SUM
				FROM DXDIR_INPUT
				INNER JOIN DXDIR_INPUT_CATEGORY
				ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
				INNER JOIN DXDIR_INPUT_DESTINATION
				ON DXDIR_INPUT.ID = DXDIR_INPUT_DESTINATION.INPUT_ID
				WHERE DXDIR_INPUT_DESTINATION.DESTINATION_CODE = @sDESTINATION_CODE AND
				      DXDIR_INPUT.ASSESSMENT_CODE = @sASSESSMENT_CODE AND
					  DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID
			) S
		ON (T.ASSESSMENT_CODE = @sASSESSMENT_CODE AND
			T.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND
			T.OUTPUT_CATEGORY_ID = @outputCategoryId AND
			T.DESTINATION_CODE = @sDESTINATION_CODE)
		WHEN MATCHED THEN 
			UPDATE
			SET COST = S.COST_SUM,
				WEIGHT = S.MASS_SUM
		WHEN NOT MATCHED THEN
			INSERT (ID,
					ASSESSMENT_CODE,
					OUTPUT_CATEGORY_ID,
					DESTINATION_CODE,
					ASSESSMENT_LC_STAGE_ID,
					COST,
					WEIGHT,
					SORT_ORDER)
			VALUES (@outputId,
					@sASSESSMENT_CODE,
					@outputCategoryId,
					@sDESTINATION_CODE,
					@dcASSESSMENT_LC_STAGE_ID,
					S.COST_SUM,
					S.MASS_SUM,
					@destinationSortOrder);
					
		IF EXISTS (SELECT 1
				   FROM DXDIR_OUTPUT
				   WHERE ASSESSMENT_CODE		= @sASSESSMENT_CODE				AND
						 ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID		AND
						 OUTPUT_CATEGORY_ID		= @outputCategoryId				AND
						 DESTINATION_CODE		= @sDESTINATION_CODE			AND
						 ISNULL(COST, 0) = 0 AND ISNULL(WEIGHT, 0) = 0)
		BEGIN
			DELETE FROM DXDIR_OUTPUT
			WHERE ASSESSMENT_CODE		  = @sASSESSMENT_CODE			AND
				  ASSESSMENT_LC_STAGE_ID  = @dcASSESSMENT_LC_STAGE_ID	AND
				  OUTPUT_CATEGORY_ID	  = @outputCategoryId			AND
				  DESTINATION_CODE		  = @sDESTINATION_CODE;
		END
	END;

END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_OUTPUT$ManageWastewaterRow'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_OUTPUT$ManageWastewaterRow]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_OUTPUT$ManageWastewaterRow]
	@sASSESSMENT_CODE VARCHAR(32),
    @dcASSESSMENT_LC_STAGE_ID BIGINT
AS
BEGIN
	DECLARE @v_requiredWasterwaterRow BIT = 0,
			@v_wastewaterPresent BIT = 0,
			@nextSeqValue_DXDIR_OUTPUT_SEQ BIGINT;

    WITH inputsWithWaterOutCats AS
    (
        -- Food and Non-food inputs - output categories
        SELECT (CASE WHEN DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
                    THEN 4
                ELSE 6 END) AS OUTPUT_CATEGORY_ID
        FROM DXDIR_INPUT
            INNER JOIN DXDIR_INPUT_CATEGORY
            ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
        WHERE DXDIR_INPUT.INEDIBLE_PARTS < 1.0 
            AND DXDIR_INPUT.ASSESSMENT_CODE = @sASSESSMENT_CODE AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID
            AND DXDIR_INPUT.MAT_PLANT = 'NONE'
            AND DXDIR_INPUT.MAT_CODE = (CASE WHEN DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
                                        THEN 'DIR_FOOD_WATER'
                                        ELSE 'DIR_NONFOOD_WATER' END)
        UNION
        -- Inedible parts inputs - output categories
        SELECT 5 AS OUTPUT_CATEGORY_ID
        FROM DXDIR_INPUT
            INNER JOIN DXDIR_INPUT_CATEGORY
            ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
        WHERE DXDIR_INPUT.INEDIBLE_PARTS > 0.0
            AND DXDIR_INPUT.ASSESSMENT_CODE = @sASSESSMENT_CODE AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
            AND DXDIR_INPUT.MAT_PLANT = 'NONE'
            AND DXDIR_INPUT.MAT_CODE = 'DIR_FOOD_WATER'
    ),
    destOutCategoriesWithSewer AS
    (
        -- sewer input destinations - output categories
        SELECT DISTINCT INPT_DEST.OUTPUT_CATEGORY_ID 
        FROM DXDIR_INPUT_DESTINATION INPT_DEST
            INNER JOIN DXDIR_INPUT INPT
            ON INPT_DEST.INPUT_ID = INPT.ID
        WHERE INPT_DEST.DESTINATION_CODE = 'SEWER'
        AND INPT.ASSESSMENT_CODE = @sASSESSMENT_CODE AND INPT.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID
    )
    SELECT @v_requiredWasterwaterRow = 1
    FROM inputsWithWaterOutCats
    WHERE OUTPUT_CATEGORY_ID NOT IN (SELECT OUTPUT_CATEGORY_ID FROM destOutCategoriesWithSewer);

    SELECT @v_wastewaterPresent = (CASE WHEN COUNT(*) > 0 THEN 1 ELSE 0 END)
    FROM DXDIR_OUTPUT
    WHERE ASSESSMENT_CODE = @sASSESSMENT_CODE AND ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID
    AND OUTPUT_CATEGORY_ID = 7;

	IF (@v_requiredWasterwaterRow = 1 AND @v_wastewaterPresent = 0)
		BEGIN
			EXECUTE DX_PK_UTILITY_SQLSRV$db_sp_get_next_sequence_value null, null, N'DXDIR_OUTPUT_SEQ', @nextSeqValue_DXDIR_OUTPUT_SEQ OUTPUT

			INSERT INTO DXDIR_OUTPUT
			(ID,
			 ASSESSMENT_CODE,
			 OUTPUT_CATEGORY_ID,
			 ASSESSMENT_LC_STAGE_ID)
			VALUES 
			(@nextSeqValue_DXDIR_OUTPUT_SEQ,
			 @sASSESSMENT_CODE,
			 7,
			 @dcASSESSMENT_LC_STAGE_ID);
		END
	ELSE IF (@v_requiredWasterwaterRow = 0 AND @v_wastewaterPresent = 1)
		BEGIN
			DELETE FROM DXDIR_OUTPUT
			WHERE ASSESSMENT_CODE = @sASSESSMENT_CODE AND ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND OUTPUT_CATEGORY_ID = 7;
		END;
END
GO
