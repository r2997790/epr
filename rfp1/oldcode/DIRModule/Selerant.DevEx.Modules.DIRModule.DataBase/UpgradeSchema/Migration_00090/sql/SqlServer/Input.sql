IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$InsertRecord'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$InsertRecord]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$InsertRecord]
	@dcID BIGINT,
	@sASSESSMENT_CODE VARCHAR(32),
	@dcINPUT_CATEGORY_ID BIGINT,
	@dcASSESSMENT_LC_STAGE_ID BIGINT,
	@sMAT_PLANT VARCHAR(20),
	@sMAT_CODE VARCHAR(32),
	@dcPART_OF_PRODUCT_COPRODUCT TINYINT,
	@dcPACKAGING TINYINT,
	@dcMASS NUMERIC(19,3),
	@dcCOST NUMERIC(24,4),
	@dcINEDIBLE_PARTS NUMERIC(3,2),
	@dcMEASUREMENT TINYINT,
	@sPRODUCT_SOURCE VARCHAR(64),
	@dcCATEGORY_SORT_ORDER INT,
	@dcINPUT_SORT_ORDER INT
AS
BEGIN
	INSERT [dbo].[DXDIR_INPUT]
	(
		[ID],
		[ASSESSMENT_CODE],
		[INPUT_CATEGORY_ID],
		[ASSESSMENT_LC_STAGE_ID],
		[MAT_PLANT],
		[MAT_CODE],
		[PART_OF_PRODUCT_COPRODUCT],
		[PACKAGING],
		[MASS],
		[COST],
		[INEDIBLE_PARTS],
		[MEASUREMENT],
		[PRODUCT_SOURCE],
		[CATEGORY_SORT_ORDER],
		[INPUT_SORT_ORDER]
	)
	VALUES
	(
		@dcID,
		@sASSESSMENT_CODE,
		@dcINPUT_CATEGORY_ID,
		@dcASSESSMENT_LC_STAGE_ID,
		@sMAT_PLANT,
		@sMAT_CODE,
		@dcPART_OF_PRODUCT_COPRODUCT,
		@dcPACKAGING,
		@dcMASS,
		@dcCOST,
		@dcINEDIBLE_PARTS,
		@dcMEASUREMENT,
		@sPRODUCT_SOURCE,
		@dcCATEGORY_SORT_ORDER,
		@dcINPUT_SORT_ORDER
	)
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$UpdateRecord'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$UpdateRecord]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$UpdateRecord]
	@dcID BIGINT,
	@sASSESSMENT_CODE VARCHAR(32),
	@dcINPUT_CATEGORY_ID BIGINT,
	@dcASSESSMENT_LC_STAGE_ID BIGINT,
	@sMAT_PLANT VARCHAR(20),
	@sMAT_CODE VARCHAR(32),
	@dcPART_OF_PRODUCT_COPRODUCT TINYINT,
	@dcPACKAGING TINYINT,
	@dcMASS NUMERIC(19,3),
	@dcCOST NUMERIC(24,4),
	@dcINEDIBLE_PARTS NUMERIC(3,2),
	@dcMEASUREMENT TINYINT,
	@sPRODUCT_SOURCE VARCHAR(64),
	@dcCATEGORY_SORT_ORDER INT,
	@dcINPUT_SORT_ORDER INT
AS
BEGIN
	UPDATE [dbo].[DXDIR_INPUT]
	SET 
		[ASSESSMENT_CODE] = @sASSESSMENT_CODE,
		[INPUT_CATEGORY_ID] = @dcINPUT_CATEGORY_ID,
		[ASSESSMENT_LC_STAGE_ID] = @dcASSESSMENT_LC_STAGE_ID,
		[MAT_PLANT] = @sMAT_PLANT,
		[MAT_CODE] = @sMAT_CODE,
		[PART_OF_PRODUCT_COPRODUCT] = @dcPART_OF_PRODUCT_COPRODUCT,
		[PACKAGING] = @dcPACKAGING,
		[MASS] = @dcMASS,
		[COST] = @dcCOST,
		[INEDIBLE_PARTS] = @dcINEDIBLE_PARTS,
		[MEASUREMENT] = @dcMEASUREMENT,
		[PRODUCT_SOURCE] = @sPRODUCT_SOURCE,
		[CATEGORY_SORT_ORDER] = @dcCATEGORY_SORT_ORDER,
		[INPUT_SORT_ORDER] = @dcINPUT_SORT_ORDER
	WHERE [ID] = @dcID
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$DeleteRecord'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$DeleteRecord]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$DeleteRecord]
	@dcID BIGINT
AS
BEGIN
	DELETE FROM [dbo].[DXDIR_INPUT]
	WHERE [ID] = @dcID
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$DeleteCascadeOutputParent'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$DeleteCascadeOutputParent]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$DeleteCascadeOutputParent] 
	@dcINPUT_ID DX_NumberTable readonly,
	@sASSESSMENT_CODE VARCHAR(32),
	@dcASSESSMENT_LC_STAGE_ID BIGINT
AS
BEGIN
	WITH cteDeleteRows AS
	(
		SELECT DISTINCT  OUTPUT_CATEGORY_ID, DESTINATION_CODE
		FROM DXDIR_OUTPUT
		WHERE ASSESSMENT_CODE = @sASSESSMENT_CODE
			AND ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID
			AND INPUT_ID IN (SELECT CONTENT FROM @dcINPUT_ID)	
		EXCEPT
		SELECT DISTINCT OUTPUT_CATEGORY_ID, DESTINATION_CODE
		FROM DXDIR_OUTPUT
		WHERE ASSESSMENT_CODE = @sASSESSMENT_CODE
			AND ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID
			AND INPUT_ID NOT IN (SELECT CONTENT FROM @dcINPUT_ID)
	)
	DELETE O FROM DXDIR_OUTPUT O INNER JOIN cteDeleteRows DR
			ON  O.DESTINATION_CODE = DR.DESTINATION_CODE
			AND O.OUTPUT_CATEGORY_ID = DR.OUTPUT_CATEGORY_ID
	WHERE ASSESSMENT_CODE = @sASSESSMENT_CODE
		AND ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID
        AND INPUT_ID IS NULL;
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$GetNextId'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$GetNextId]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$GetNextId]
	@dcNextId BIGINT OUTPUT 
AS
BEGIN
	SET @dcNextId = NULL

	DECLARE @dbname NVARCHAR(128) = db_name()

	EXEC DX_PK_UTILITY_SQLSRV$db_sp_get_next_sequence_value @dbname
		,N'dbo'
		,N'DXDIR_INPUT_SEQ'
		,@dcNextId OUTPUT
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$UpdateCascadeOnOutput'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$UpdateCascadeOnOutput]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$UpdateCascadeOnOutput]
	@sASSESSMENT_CODE VARCHAR(32),
	@dcASSESSMENT_LC_STAGE_ID BIGINT
AS
BEGIN
	exec [dbo].[DXDIR_PK_INPUT$UpdateDataInOutput] @sASSESSMENT_CODE, @dcASSESSMENT_LC_STAGE_ID, 'PRODUCT', 1;
	exec [dbo].[DXDIR_PK_INPUT$UpdateDataInOutput] @sASSESSMENT_CODE, @dcASSESSMENT_LC_STAGE_ID, 'PRODUCT_2', 1;
	exec [dbo].[DXDIR_PK_INPUT$UpdateDataInOutput] @sASSESSMENT_CODE, @dcASSESSMENT_LC_STAGE_ID, 'PRODUCT_3', 1;
	exec [dbo].[DXDIR_PK_INPUT$UpdateDataInOutput] @sASSESSMENT_CODE, @dcASSESSMENT_LC_STAGE_ID, 'COPRODUCT', 2;
	exec [dbo].[DXDIR_PK_INPUT$UpdateDataInOutput] @sASSESSMENT_CODE, @dcASSESSMENT_LC_STAGE_ID, 'COPRODUCT_2', 2;
	exec [dbo].[DXDIR_PK_INPUT$UpdateDataInOutput] @sASSESSMENT_CODE, @dcASSESSMENT_LC_STAGE_ID, 'FOOD_RESCUE', 3;
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$UpdateDataInOutput'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$UpdateDataInOutput]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$UpdateDataInOutput]
	@sASSESSMENT_CODE VARCHAR(32),
	@dcASSESSMENT_LC_STAGE_ID BIGINT,
	@sDESTINATION_CODE VARCHAR(32),
	@dcOUTPUT_CATEGORY_ID BIGINT
AS
BEGIN
	MERGE INTO DXDIR_OUTPUT T
	USING (SELECT ISNULL(SUM((DXDIR_INPUT_DESTINATION.PERCENTAGE / 100.0) * DXDIR_INPUT.COST *
				  (CASE
					WHEN DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
						THEN 1 - DXDIR_INPUT.INEDIBLE_PARTS
					ELSE
						1
				  END)),0) as COST_SUM,
				  ISNULL(SUM((DXDIR_INPUT_DESTINATION.PERCENTAGE / 100.0) * DXDIR_INPUT.MASS *
				  (CASE
					WHEN DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
						THEN 1 - DXDIR_INPUT.INEDIBLE_PARTS
					ELSE
						1
				  END)),0) as MASS_SUM
		  FROM DXDIR_INPUT
			   INNER JOIN DXDIR_INPUT_CATEGORY
			   ON DXDIR_INPUT.INPUT_CATEGORY_ID	 = DXDIR_INPUT_CATEGORY.ID
			   INNER JOIN DXDIR_INPUT_DESTINATION
			   ON DXDIR_INPUT.ID					 = DXDIR_INPUT_DESTINATION.INPUT_ID
		  WHERE DXDIR_INPUT_DESTINATION.DESTINATION_CODE = @sDESTINATION_CODE AND
				DXDIR_INPUT.ASSESSMENT_CODE				 = @sASSESSMENT_CODE AND
				DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID		 = @dcASSESSMENT_LC_STAGE_ID AND
				DXDIR_INPUT.PART_OF_PRODUCT_COPRODUCT	 = 1
		 ) S
	ON (T.ASSESSMENT_CODE		 = @sASSESSMENT_CODE AND
		T.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND
		T.OUTPUT_CATEGORY_ID	 = @dcOUTPUT_CATEGORY_ID AND
		T.DESTINATION_CODE       = @sDESTINATION_CODE)
	WHEN MATCHED THEN 
		 UPDATE
		 SET COST	 = S.COST_SUM,
			 WEIGHT	 = S.MASS_SUM;

	IF EXISTS (SELECT 1
			   FROM DXDIR_OUTPUT
			   WHERE ASSESSMENT_CODE = @sASSESSMENT_CODE AND
				     ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND
				     OUTPUT_CATEGORY_ID	 = @dcOUTPUT_CATEGORY_ID AND
				     DESTINATION_CODE = @sDESTINATION_CODE AND
					 ISNULL(COST, 0) = 0 AND ISNULL(WEIGHT, 0) = 0)
	BEGIN
		DELETE FROM DXDIR_OUTPUT
		WHERE ASSESSMENT_CODE = @sASSESSMENT_CODE AND
			  ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND
			  OUTPUT_CATEGORY_ID	 = @dcOUTPUT_CATEGORY_ID AND
			  DESTINATION_CODE = @sDESTINATION_CODE;
	END

END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$UpdateOutputAnotherDestination'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$UpdateOutputAnotherDestination]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$UpdateOutputAnotherDestination]
	@sASSESSMENT_CODE VARCHAR(32),
	@dcASSESSMENT_LC_STAGE_ID BIGINT,
	@dcINPUT_ID BIGINT
AS
BEGIN
	MERGE INTO DXDIR_OUTPUT T
	USING (SELECT DI.ID INPUT_ID, DESTINATION_CODE,
				  OUTPUT_CATEGORY_ID,
				  ISNULL(((DD.PERCENTAGE / 100.0) * DI.COST *
				  (CASE
					WHEN DIG.TYPE = 'FOOD'
					THEN
						(CASE
						WHEN DD.OUTPUT_CATEGORY_ID = 4
							THEN 1 - DI.INEDIBLE_PARTS
						ELSE 
							DI.INEDIBLE_PARTS
						END)
					ELSE
						1
				  END)),0) as COST_SUM,
				  ISNULL(((DD.PERCENTAGE / 100.0) * DI.MASS *
				  (CASE
					WHEN DIG.TYPE = 'FOOD'
					THEN
						(CASE
						WHEN DD.OUTPUT_CATEGORY_ID = 4
							THEN 1 - DI.INEDIBLE_PARTS
						ELSE 
							DI.INEDIBLE_PARTS
						END)
					ELSE
						1
				  END)),0) as MASS_SUM
		  FROM DXDIR_INPUT DI 
			   INNER JOIN DXDIR_INPUT_DESTINATION DD
			   ON DI.ID				 = DD.INPUT_ID 
			   INNER JOIN DXDIR_INPUT_CATEGORY DIG 
			   ON DIG.ID			 = DI.INPUT_CATEGORY_ID AND 
			   DI.ASSESSMENT_CODE	 = DIG.ASSESSMENT_CODE
		  WHERE DI.ID						 = @dcINPUT_ID AND
				DI.ASSESSMENT_CODE			 = @sASSESSMENT_CODE AND 
				DI.ASSESSMENT_LC_STAGE_ID	 = @dcASSESSMENT_LC_STAGE_ID AND
				DD.DESTINATION_CODE			 NOT IN ('PRODUCT', 'PRODUCT_2', 'PRODUCT_3', 'COPRODUCT', 'COPRODUCT_2', 'FOOD_RESCUE')
		) S
	ON (T.DESTINATION_CODE		 = S.DESTINATION_CODE AND
		T.ASSESSMENT_CODE		 = @sASSESSMENT_CODE AND
		T.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND
		T.OUTPUT_CATEGORY_ID	 = S.OUTPUT_CATEGORY_ID AND
		T.INPUT_ID				 = S.INPUT_ID)
	WHEN MATCHED THEN
		 UPDATE
		 SET COST	 = S.COST_SUM,
			 WEIGHT	 = S.MASS_SUM;
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$NextSortOrder'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$NextSortOrder]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$NextSortOrder]
	@sASSESSMENT_CODE VARCHAR(32),
    @dcINPUT_CATEGORY_ID BIGINT,
	@dcASSESSMENT_LC_STAGE_ID BIGINT,
	@dcCATEGORY_SORT_ORDER INT OUTPUT,
    @dcINPUT_SORT_ORDER INT OUTPUT  
AS
BEGIN
    SET @dcCATEGORY_SORT_ORDER = (SELECT TOP 1 CATEGORY_SORT_ORDER
								  FROM DXDIR_INPUT
								  WHERE ASSESSMENT_CODE = @sASSESSMENT_CODE
								    AND INPUT_CATEGORY_ID = @dcINPUT_CATEGORY_ID
								    AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID);

	IF @dcCATEGORY_SORT_ORDER IS NULL
	BEGIN
	    SELECT @dcCATEGORY_SORT_ORDER = ISNULL(MAX(CATEGORY_SORT_ORDER), 0) + 1
		FROM DXDIR_INPUT
		WHERE ASSESSMENT_CODE = @sASSESSMENT_CODE
		AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID;
	END;
        
    SELECT @dcINPUT_SORT_ORDER = ISNULL(MAX(INPUT_SORT_ORDER), 0) + 1
	FROM DXDIR_INPUT
	WHERE ASSESSMENT_CODE = @sASSESSMENT_CODE
    AND INPUT_CATEGORY_ID = @dcINPUT_CATEGORY_ID
    AND ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID;
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$UpdateDestinInediblePart'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$UpdateDestinInediblePart]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$UpdateDestinInediblePart]
	@sASSESSMENT_CODE VARCHAR(32),
	@dcASSESSMENT_LC_STAGE_ID BIGINT,
	@dcINPUT_ID BIGINT
AS
BEGIN
	DECLARE @outputCategoryDelete BIGINT;

	SELECT @outputCategoryDelete = CASE WHEN INEDIBLE_PARTS = 1 THEN 4 WHEN INEDIBLE_PARTS = 0 THEN 5 END
	FROM DXDIR_INPUT
	WHERE ASSESSMENT_CODE		 = @sASSESSMENT_CODE AND
		  ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND
		  ID					 = @dcINPUT_ID;

	DELETE FROM DXDIR_INPUT_DESTINATION
	WHERE INPUT_ID			 = @dcINPUT_ID AND
		  OUTPUT_CATEGORY_ID = @outputCategoryDelete;

	---Deleting destination parents in output table
	WITH cteDeleteRows AS
	(
		SELECT DISTINCT DESTINATION_CODE
		FROM DXDIR_OUTPUT
		WHERE ASSESSMENT_CODE		 = @sASSESSMENT_CODE AND
			  ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND
			  INPUT_ID				 = @dcINPUT_ID AND
			  OUTPUT_CATEGORY_ID	 =  @outputCategoryDelete
		EXCEPT
		SELECT DISTINCT DESTINATION_CODE
		FROM DXDIR_OUTPUT
		WHERE ASSESSMENT_CODE		 = @sASSESSMENT_CODE AND
			  ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND
			  INPUT_ID				 <> @dcINPUT_ID AND
			  OUTPUT_CATEGORY_ID	 = @outputCategoryDelete
	)
	DELETE O 
	FROM DXDIR_OUTPUT O 
	INNER JOIN cteDeleteRows DR ON
			   O.DESTINATION_CODE	 = DR.DESTINATION_CODE AND 
			   O.OUTPUT_CATEGORY_ID	 =@outputCategoryDelete
	WHERE ASSESSMENT_CODE		 = @sASSESSMENT_CODE AND 
		  ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND 
		  INPUT_ID IS NULL;

	DELETE FROM DXDIR_OUTPUT
	WHERE ASSESSMENT_CODE		 = @sASSESSMENT_CODE AND
		  ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND
		  OUTPUT_CATEGORY_ID	 = @outputCategoryDelete AND
		  INPUT_ID				 = @dcINPUT_ID;
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$GetCarriedInputsToNextStage'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$GetCarriedInputsToNextStage]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$GetCarriedInputsToNextStage]
	@sASSESSMENT_CODE VARCHAR(32),
	@dcASSESSMENT_LC_STAGE_ID BIGINT
AS
BEGIN
	SELECT 
		ROW_NUMBER() OVER (ORDER BY (SELECT null)) AS ID,
		I.ASSESSMENT_CODE,
		I.INPUT_CATEGORY_ID,
		I.ASSESSMENT_LC_STAGE_ID,
		I.MAT_PLANT,
		I.MAT_CODE,
		I.PACKAGING,
		(I.MASS * ((1 - I.INEDIBLE_PARTS))* (D.PERCENTAGE / 100)) AS MASS,
		(I.COST * ((1 - I.INEDIBLE_PARTS))* (D.PERCENTAGE / 100)) AS COST,
		I.CATEGORY_SORT_ORDER,
		ROW_NUMBER() OVER (ORDER BY (SELECT null)) AS INPUT_SORT_ORDER,
		D.DESTINATION_CODE AS PRODUCT_SOURCE,
		MEASUREMENT
	FROM DXDIR_INPUT I
	JOIN DXDIR_INPUT_DESTINATION D
	ON I.ID = D.INPUT_ID
	WHERE I.ASSESSMENT_CODE = @sASSESSMENT_CODE AND 
	      I.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND 
	      D.DESTINATION_CODE LIKE 'PRODUCT%' AND
	      D.PERCENTAGE > 0
	ORDER BY CATEGORY_SORT_ORDER, INPUT_SORT_ORDER, PRODUCT_SOURCE;
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT$HasInputsToCarryOverCount'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT$HasInputsToCarryOverCount]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT$HasInputsToCarryOverCount]
	@sASSESSMENT_CODE VARCHAR(32),
	@dcASSESSMENT_LC_STAGE_ID_CUR BIGINT,
	@dcASSESSMENT_LC_STAGE_ID_NEXT VARCHAR(32),
	@dcRESULT_CURRENT BIGINT OUTPUT,
	@dcRESULT_NEXT BIGINT OUTPUT
AS
BEGIN
	SELECT 
	 	@dcRESULT_CURRENT = COUNT(*) 
	FROM DXDIR_INPUT I
	JOIN DXDIR_INPUT_DESTINATION D
	ON I.ID = D.INPUT_ID
	WHERE I.ASSESSMENT_CODE			 = @sASSESSMENT_CODE				AND 
          I.ASSESSMENT_LC_STAGE_ID	 = @dcASSESSMENT_LC_STAGE_ID_CUR	AND 
          D.DESTINATION_CODE LIKE 'PRODUCT%'							AND
          D.PERCENTAGE > 0;
    
	SELECT 
		@dcRESULT_NEXT = COUNT(*) 
	FROM DXDIR_INPUT 
	WHERE ASSESSMENT_CODE		 = @sASSESSMENT_CODE AND
		  ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID_NEXT;
END
GO