CREATE OR REPLACE PACKAGE DXDIR_PK_INPUT AS

	TYPE t_cursor IS REF CURSOR;

	PROCEDURE InsertRecord
	(
		dcID NUMBER,
		sASSESSMENT_CODE VARCHAR2,
		dcINPUT_CATEGORY_ID NUMBER,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		sMAT_PLANT VARCHAR2,
		sMAT_CODE VARCHAR2,
		dcPART_OF_PRODUCT_COPRODUCT NUMBER,
		dcPACKAGING NUMBER,
		dcMASS NUMBER,
		dcCOST NUMBER,
		dcINEDIBLE_PARTS NUMBER,
		dcMEASUREMENT NUMBER,
		sPRODUCT_SOURCE VARCHAR2,
		dcCATEGORY_SORT_ORDER NUMBER,
		dcINPUT_SORT_ORDER NUMBER
	);

	PROCEDURE UpdateRecord
	(
		dcID NUMBER,
		sASSESSMENT_CODE VARCHAR2,
		dcINPUT_CATEGORY_ID NUMBER,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		sMAT_PLANT VARCHAR2,
		sMAT_CODE VARCHAR2,
		dcPART_OF_PRODUCT_COPRODUCT NUMBER,
		dcPACKAGING NUMBER,
		dcMASS NUMBER,
		dcCOST NUMBER,
		dcINEDIBLE_PARTS NUMBER,
		dcMEASUREMENT NUMBER,
		sPRODUCT_SOURCE VARCHAR2,
		dcCATEGORY_SORT_ORDER NUMBER,
		dcINPUT_SORT_ORDER NUMBER
	);

	PROCEDURE DeleteRecord
	(
		dcID NUMBER
	);

	PROCEDURE DeleteCascadeOutputParent
    (
		dcINPUT_ID DX_PK_UTILITY.DX_NUMBERARRAY,
        sASSESSMENT_CODE VARCHAR,
        dcASSESSMENT_LC_STAGE_ID NUMBER
    );

	PROCEDURE GetNextId
	(
        dcNextID OUT NUMBER
    );

	PROCEDURE NextSortOrder
	(
		sASSESSMENT_CODE VARCHAR2,
        dcINPUT_CATEGORY_ID NUMBER,
        dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcCATEGORY_SORT_ORDER OUT NUMBER,
        dcINPUT_SORT_ORDER OUT NUMBER
	);
    
	PROCEDURE UpdateCascadeOnOutput
    (
        sASSESSMENT_CODE VARCHAR2,
        dcASSESSMENT_LC_STAGE_ID NUMBER
    ); 
	
    PROCEDURE UpdateDataInOutput
    (
        sASSESSMENT_CODE VARCHAR2,
        dcASSESSMENT_LC_STAGE_ID NUMBER,
        sDESTINATION_CODE VARCHAR2,
        dcOUTPUT_CATEGORY_ID NUMBER
    );

	PROCEDURE UpdateOutputAnotherDestination
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcINPUT_ID NUMBER
    );

	PROCEDURE UpdateDestinInediblePart
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcINPUT_ID NUMBER
	);

	PROCEDURE GetCarriedInputsToNextStage
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		po_cur IN OUT t_cursor
	);
    
    PROCEDURE HasInputsToCarryOverCount
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID_CUR NUMBER,
		dcASSESSMENT_LC_STAGE_ID_NEXT NUMBER,
		dcRESULT_CURRENT OUT NUMBER,
		dcRESULT_NEXT OUT NUMBER
	);
    
END;
/

CREATE OR REPLACE PACKAGE BODY DXDIR_PK_INPUT AS

	PROCEDURE InsertRecord
	(
		dcID NUMBER,
		sASSESSMENT_CODE VARCHAR2,
		dcINPUT_CATEGORY_ID NUMBER,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		sMAT_PLANT VARCHAR2,
		sMAT_CODE VARCHAR2,
		dcPART_OF_PRODUCT_COPRODUCT NUMBER,
		dcPACKAGING NUMBER,
		dcMASS NUMBER,
		dcCOST NUMBER,
		dcINEDIBLE_PARTS NUMBER,
		dcMEASUREMENT NUMBER,
        sPRODUCT_SOURCE VARCHAR2,
		dcCATEGORY_SORT_ORDER NUMBER,
		dcINPUT_SORT_ORDER NUMBER
	)
	IS
	BEGIN
		INSERT INTO DXDIR_INPUT
		(
			ID,
			ASSESSMENT_CODE,
			INPUT_CATEGORY_ID,
			ASSESSMENT_LC_STAGE_ID,
			MAT_PLANT,
			MAT_CODE,
			PART_OF_PRODUCT_COPRODUCT,
			PACKAGING,
			MASS,
			COST,
			INEDIBLE_PARTS,
			MEASUREMENT,
            PRODUCT_SOURCE,
			CATEGORY_SORT_ORDER,
			INPUT_SORT_ORDER
		)
		VALUES
		(
			dcID,
			sASSESSMENT_CODE,
			dcINPUT_CATEGORY_ID,
			dcASSESSMENT_LC_STAGE_ID,
			sMAT_PLANT,
			sMAT_CODE,
			dcPART_OF_PRODUCT_COPRODUCT,
			dcPACKAGING,
			dcMASS,
			dcCOST,
			dcINEDIBLE_PARTS,
			dcMEASUREMENT,
            sPRODUCT_SOURCE,
			dcCATEGORY_SORT_ORDER,
			dcINPUT_SORT_ORDER
		);
	END;

	PROCEDURE UpdateRecord
	(
		dcID NUMBER,
		sASSESSMENT_CODE VARCHAR2,
		dcINPUT_CATEGORY_ID NUMBER,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		sMAT_PLANT VARCHAR2,
		sMAT_CODE VARCHAR2,
		dcPART_OF_PRODUCT_COPRODUCT NUMBER,
		dcPACKAGING NUMBER,
		dcMASS NUMBER,
		dcCOST NUMBER,
		dcINEDIBLE_PARTS NUMBER,
		dcMEASUREMENT NUMBER,
        sPRODUCT_SOURCE VARCHAR2,
		dcCATEGORY_SORT_ORDER NUMBER,
		dcINPUT_SORT_ORDER NUMBER
	)
	IS
	BEGIN
		UPDATE DXDIR_INPUT
		SET 
			ASSESSMENT_CODE = sASSESSMENT_CODE,
			INPUT_CATEGORY_ID = dcINPUT_CATEGORY_ID,
			ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID,
			MAT_PLANT = sMAT_PLANT,
			MAT_CODE = sMAT_CODE,
			PART_OF_PRODUCT_COPRODUCT = dcPART_OF_PRODUCT_COPRODUCT,
			PACKAGING = dcPACKAGING,
			MASS = dcMASS,
			COST = dcCOST,
			INEDIBLE_PARTS = dcINEDIBLE_PARTS,
			MEASUREMENT = dcMEASUREMENT,
            PRODUCT_SOURCE = sPRODUCT_SOURCE,
			CATEGORY_SORT_ORDER = dcCATEGORY_SORT_ORDER,
			INPUT_SORT_ORDER = dcINPUT_SORT_ORDER
		WHERE
			ID = dcID;
	END;

	PROCEDURE DeleteRecord
	(
		dcID NUMBER
	)
	IS
	BEGIN
		DELETE FROM DXDIR_INPUT
		WHERE
			ID = dcID;
	END;

    PROCEDURE DeleteCascadeOutputParent
    (
        dcINPUT_ID DX_PK_UTILITY.DX_NUMBERARRAY,
        sASSESSMENT_CODE VARCHAR,
        dcASSESSMENT_LC_STAGE_ID NUMBER
    )
    IS
        dcInputIds DX_NumberTable;
    BEGIN
        dcInputIds := DX_PK_Utility.ArrayToTable(dcINPUT_ID);

        DELETE FROM DXDIR_OUTPUT
        WHERE ASSESSMENT_CODE = sASSESSMENT_CODE
			AND ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID
			AND INPUT_ID IS NULL
			AND (OUTPUT_CATEGORY_ID, DESTINATION_CODE) IN
			(
				WITH cteHAVING_CHILDREN AS
				(
					SELECT DISTINCT OUTPUT_CATEGORY_ID, DESTINATION_CODE     
					FROM DXDIR_OUTPUT
					WHERE ASSESSMENT_CODE = sASSESSMENT_CODE
					AND ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID
					AND INPUT_ID NOT IN (SELECT COLUMN_VALUE FROM THE(SELECT CAST(dcInputIds AS DX_NumberTable) FROM DUAL))
				)
				SELECT DISTINCT OUTPUT_CATEGORY_ID, DESTINATION_CODE
				FROM DXDIR_OUTPUT
				WHERE ASSESSMENT_CODE = sASSESSMENT_CODE
					AND ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID
					AND INPUT_ID IN (SELECT COLUMN_VALUE FROM THE(SELECT CAST(dcInputIds AS DX_NumberTable) FROM DUAL))
					AND (OUTPUT_CATEGORY_ID, DESTINATION_CODE)
						NOT IN (SELECT HC.OUTPUT_CATEGORY_ID, HC.DESTINATION_CODE FROM cteHAVING_CHILDREN HC)
			);
    END;

	PROCEDURE GetNextId
	(
        dcNextID OUT NUMBER
    )
	IS
	BEGIN
		SELECT DXDIR_INPUT_SEQ.NEXTVAL INTO dcNextID FROM DUAL;
    END;
  
	PROCEDURE NextSortOrder
	(
		sASSESSMENT_CODE VARCHAR2,
        dcINPUT_CATEGORY_ID NUMBER,
        dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcCATEGORY_SORT_ORDER OUT NUMBER,
        dcINPUT_SORT_ORDER OUT NUMBER
	)
	IS
	BEGIN
		BEGIN
			SELECT CATEGORY_SORT_ORDER
			INTO dcCATEGORY_SORT_ORDER
			FROM DXDIR_INPUT
			WHERE ASSESSMENT_CODE = sASSESSMENT_CODE
			  AND INPUT_CATEGORY_ID = dcINPUT_CATEGORY_ID
			  AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID
			  AND ROWNUM = 1;
		EXCEPTION
			WHEN NO_DATA_FOUND
		THEN
			SELECT NVL(MAX(CATEGORY_SORT_ORDER), 0) + 1
			INTO dcCATEGORY_SORT_ORDER
			FROM DXDIR_INPUT
			WHERE ASSESSMENT_CODE = sASSESSMENT_CODE
				AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID;
		END;
        
        SELECT NVL(MAX(INPUT_SORT_ORDER), 0) + 1
		INTO dcINPUT_SORT_ORDER
		FROM DXDIR_INPUT
		WHERE ASSESSMENT_CODE = sASSESSMENT_CODE
          AND INPUT_CATEGORY_ID = dcINPUT_CATEGORY_ID
          AND ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID;
	END;

    PROCEDURE UpdateCascadeOnOutput
    (
        sASSESSMENT_CODE VARCHAR2,
        dcASSESSMENT_LC_STAGE_ID NUMBER
    )
    IS
    BEGIN
        DXDIR_PK_INPUT.UpdateDataInOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID , 'PRODUCT', 1);
		DXDIR_PK_INPUT.UpdateDataInOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID , 'PRODUCT_2', 1);
		DXDIR_PK_INPUT.UpdateDataInOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID , 'PRODUCT_3', 1);
        DXDIR_PK_INPUT.UpdateDataInOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID , 'COPRODUCT', 2);
		DXDIR_PK_INPUT.UpdateDataInOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID , 'COPRODUCT_2', 2);
        DXDIR_PK_INPUT.UpdateDataInOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID , 'FOOD_RESCUE', 3);
    END;

	PROCEDURE UpdateDataInOutput
    (
        sASSESSMENT_CODE VARCHAR2,
        dcASSESSMENT_LC_STAGE_ID NUMBER,
        sDESTINATION_CODE VARCHAR2,
        dcOUTPUT_CATEGORY_ID NUMBER
    )
    IS
		v_removeOutputRow NUMBER;
    BEGIN
		MERGE INTO DXDIR_OUTPUT T
		USING (SELECT NVL (SUM((DD.PERCENTAGE / 100.0) * DI.COST *
					  (CASE
						WHEN DC.TYPE = 'FOOD'
							THEN 1 - DI.INEDIBLE_PARTS
						ELSE
							1
					  END)),0) as COST_SUM,
					  NVL (SUM((DD.PERCENTAGE / 100.0) * DI.MASS *
					  (CASE
						WHEN DC.TYPE = 'FOOD'
							THEN 1 - DI.INEDIBLE_PARTS
						ELSE
							1
					  END)),0) as MASS_SUM
			  FROM DXDIR_INPUT DI 
				   INNER JOIN DXDIR_INPUT_CATEGORY DC
				   ON DI.INPUT_CATEGORY_ID	 = DC.ID
				   INNER JOIN DXDIR_INPUT_DESTINATION DD
				   ON DI.ID					 = DD.INPUT_ID
			  WHERE DI.ASSESSMENT_CODE			 = sASSESSMENT_CODE AND
					DI.ASSESSMENT_LC_STAGE_ID	 = dcASSESSMENT_LC_STAGE_ID AND
					DD.DESTINATION_CODE			 = sDESTINATION_CODE AND
					DI.PART_OF_PRODUCT_COPRODUCT = 1
			 ) S
		ON (T.ASSESSMENT_CODE			 = sASSESSMENT_CODE AND
			T.ASSESSMENT_LC_STAGE_ID	 = dcASSESSMENT_LC_STAGE_ID AND
			T.OUTPUT_CATEGORY_ID		 = dcOUTPUT_CATEGORY_ID AND
			T.DESTINATION_CODE           = sDESTINATION_CODE)
		WHEN MATCHED THEN
			 UPDATE
			 SET COST	 = S.COST_SUM,
				 WEIGHT	 = S.MASS_SUM;
		
		-- Remove Non-Food rows when there aren't any COST or WEIGHT calculated from destinations
		BEGIN
			SELECT CASE 
				WHEN NVL(COST, 0) = 0 AND NVL(WEIGHT, 0) = 0 
				THEN 1 
				ELSE 0 END
			INTO v_removeOutputRow
			FROM DXDIR_OUTPUT
			WHERE ASSESSMENT_CODE		   = sASSESSMENT_CODE			AND
				  ASSESSMENT_LC_STAGE_ID   = dcASSESSMENT_LC_STAGE_ID	AND
				  OUTPUT_CATEGORY_ID	   = dcOUTPUT_CATEGORY_ID		AND
				  DESTINATION_CODE         = sDESTINATION_CODE;
			EXCEPTION
				WHEN NO_DATA_FOUND THEN
					v_removeOutputRow := 0;
		END;
		
		IF (v_removeOutputRow = 1)
		THEN
			DELETE FROM DXDIR_OUTPUT
			WHERE ASSESSMENT_CODE		   = sASSESSMENT_CODE			AND
				  ASSESSMENT_LC_STAGE_ID   = dcASSESSMENT_LC_STAGE_ID	AND
				  OUTPUT_CATEGORY_ID	   = dcOUTPUT_CATEGORY_ID		AND
				  DESTINATION_CODE         = sDESTINATION_CODE;
		END IF;

    END;

    PROCEDURE UpdateOutputAnotherDestination
    (
        sASSESSMENT_CODE VARCHAR2,
        dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcINPUT_ID NUMBER
    )
    IS
    BEGIN
		MERGE INTO DXDIR_OUTPUT T
		USING (SELECT DI.ID INPUT_ID, DESTINATION_CODE,
					  OUTPUT_CATEGORY_ID,
					  NVL(((DD.PERCENTAGE / 100.0) * DI.COST *
					  (CASE
						WHEN DIG.TYPE = 'FOOD'
						THEN
							(CASE
							WHEN DD.OUTPUT_CATEGORY_ID = 4
								THEN 1 - DI.INEDIBLE_PARTS
							ELSE 
								DI.INEDIBLE_PARTS
							END)
						ELSE
							1
					  END)),0) as COST_SUM,
					  NVL(((DD.PERCENTAGE / 100.0) * DI.MASS *
					  (CASE
						WHEN DIG.TYPE = 'FOOD'
						THEN
							(CASE
							WHEN DD.OUTPUT_CATEGORY_ID = 4
								THEN 1 - DI.INEDIBLE_PARTS
							ELSE 
								DI.INEDIBLE_PARTS
							END)
						 ELSE
							1
					  END)),0) as MASS_SUM
			  FROM DXDIR_INPUT DI 
				   INNER JOIN DXDIR_INPUT_DESTINATION DD
				   ON DI.ID				 = DD.INPUT_ID 
				   INNER JOIN DXDIR_INPUT_CATEGORY DIG 
				   ON DIG.ID			 = DI.INPUT_CATEGORY_ID AND 
				   DI.ASSESSMENT_CODE	 = DIG.ASSESSMENT_CODE
			  WHERE DI.ID						 = dcINPUT_ID AND
					DI.ASSESSMENT_CODE			 = sASSESSMENT_CODE AND
					DI.ASSESSMENT_LC_STAGE_ID	 = dcASSESSMENT_LC_STAGE_ID AND
					DD.DESTINATION_CODE			 NOT IN ('PRODUCT', 'PRODUCT_2', 'PRODUCT_3', 'COPRODUCT', 'COPRODUCT_2', 'FOOD_RESCUE')
			 ) S
		ON (T.DESTINATION_CODE		 = S.DESTINATION_CODE AND
			T.ASSESSMENT_CODE		 = sASSESSMENT_CODE AND
			T.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND
			T.OUTPUT_CATEGORY_ID	 = S.OUTPUT_CATEGORY_ID AND
			T.INPUT_ID				 = S.INPUT_ID)
		WHEN MATCHED THEN
			 UPDATE
			 SET COST	 = S.COST_SUM,
				 WEIGHT	 = S.MASS_SUM;
    END;


	PROCEDURE UpdateDestinInediblePart
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcINPUT_ID NUMBER
	)
	IS
		outputCategoryDelete NUMBER;
	BEGIN
		SELECT CASE WHEN INEDIBLE_PARTS = 1 THEN 4 WHEN INEDIBLE_PARTS = 0 THEN 5 END
		INTO outputCategoryDelete
		FROM DXDIR_INPUT
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND
			  ID					 = dcINPUT_ID;

		DELETE FROM DXDIR_INPUT_DESTINATION
		WHERE INPUT_ID			 = dcINPUT_ID AND
			  OUTPUT_CATEGORY_ID = outputCategoryDelete;

		---Deleting destination parents in output table
		DELETE FROM DXDIR_OUTPUT
		WHERE ASSESSMENT_CODE = sASSESSMENT_CODE AND 
			ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
			INPUT_ID IS NULL AND 
			OUTPUT_CATEGORY_ID = outputCategoryDelete AND
			DESTINATION_CODE IN
			(
				WITH cteHAVING_CHILDREN AS 
				(
					SELECT DISTINCT DESTINATION_CODE
					FROM DXDIR_OUTPUT
					WHERE ASSESSMENT_CODE = sASSESSMENT_CODE AND 
						  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
						  INPUT_ID <> dcINPUT_ID AND
						  OUTPUT_CATEGORY_ID = outputCategoryDelete
				)
				SELECT DISTINCT DESTINATION_CODE
				FROM DXDIR_OUTPUT
				WHERE ASSESSMENT_CODE = sASSESSMENT_CODE AND 
					  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
					  INPUT_ID = dcINPUT_ID AND 
					  OUTPUT_CATEGORY_ID = outputCategoryDelete AND
					  DESTINATION_CODE NOT IN (SELECT HC.DESTINATION_CODE FROM cteHAVING_CHILDREN HC)
			);

		DELETE FROM DXDIR_OUTPUT
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND
			  OUTPUT_CATEGORY_ID	 = outputCategoryDelete AND
			  INPUT_ID				 = dcINPUT_ID;
	END;

	PROCEDURE GetCarriedInputsToNextStage
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		po_cur IN OUT t_cursor
	)
	IS
	BEGIN
		OPEN po_cur FOR
			SELECT 
				ROWNUM ID,
				I.ASSESSMENT_CODE,
				I.INPUT_CATEGORY_ID,
				I.ASSESSMENT_LC_STAGE_ID,
				I.MAT_PLANT,
				I.MAT_CODE,
				I.PACKAGING,
				(I.MASS * ((1 - I.INEDIBLE_PARTS))* (D.PERCENTAGE / 100)) AS MASS,
				(I.COST * ((1 - I.INEDIBLE_PARTS))* (D.PERCENTAGE / 100)) AS COST,
				I.CATEGORY_SORT_ORDER,
				ROWNUM INPUT_SORT_ORDER,
				D.DESTINATION_CODE AS PRODUCT_SOURCE,
				MEASUREMENT
			FROM DXDIR_INPUT I
			JOIN DXDIR_INPUT_DESTINATION D
			ON I.ID = D.INPUT_ID
			WHERE I.ASSESSMENT_CODE = sASSESSMENT_CODE AND 
                  I.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
                  D.DESTINATION_CODE LIKE 'PRODUCT%' AND
                  D.PERCENTAGE > 0
            ORDER BY CATEGORY_SORT_ORDER, INPUT_SORT_ORDER, PRODUCT_SOURCE;
	END;

	PROCEDURE HasInputsToCarryOverCount
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID_CUR NUMBER,
		dcASSESSMENT_LC_STAGE_ID_NEXT NUMBER,
		dcRESULT_CURRENT OUT NUMBER,
		dcRESULT_NEXT OUT NUMBER
	)
	IS
	BEGIN
		SELECT COUNT(*)
		INTO dcRESULT_CURRENT
		FROM DXDIR_INPUT I
		JOIN DXDIR_INPUT_DESTINATION D
		ON I.ID = D.INPUT_ID
		WHERE I.ASSESSMENT_CODE			 = sASSESSMENT_CODE				AND 
              I.ASSESSMENT_LC_STAGE_ID	 = dcASSESSMENT_LC_STAGE_ID_CUR AND 
              D.DESTINATION_CODE LIKE 'PRODUCT%'						AND
              D.PERCENTAGE > 0;
    
		SELECT COUNT(*) 
		INTO dcRESULT_NEXT 
		FROM DXDIR_INPUT 
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID_NEXT;
	END;
END;
/