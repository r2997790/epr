CREATE OR REPLACE PACKAGE DXDIR_PK_INPUT_DESTINATION AS

	TYPE t_cursor IS REF CURSOR;

	PROCEDURE InsertRecord
	(
		dcINPUT_ID NUMBER,
		sDESTINATION_CODE VARCHAR,
		dcOUTPUT_CATEGORY_ID NUMBER,
		dcPERCENTAGE NUMBER
	);

	PROCEDURE UpdateRecord
	(
		dcINPUT_ID NUMBER,
		sDESTINATION_CODE VARCHAR,
		dcOUTPUT_CATEGORY_ID NUMBER,
		dcPERCENTAGE NUMBER
	);

	PROCEDURE DeleteRecord
	(
		dcINPUT_ID NUMBER,
		sDESTINATION_CODE VARCHAR,
		dcOUTPUT_CATEGORY_ID NUMBER
	);
    
    PROCEDURE DeleteDestiantionByCodes
    (
        dcINPUT_ID NUMBER,
		sDESTINATION_CODES DX_PK_UTILITY.DX_Varchar2Array
    );

    PROCEDURE SelectFoodDestinations
	(
        sASSESSMENT_CODE VARCHAR2,
        dcASSESSMENT_LC_STAGE_ID NUMBER,
        po_cur OUT t_cursor
	);

	PROCEDURE SelectNonFoodDestinations
	(
        sASSESSMENT_CODE VARCHAR2,
        dcASSESSMENT_LC_STAGE_ID NUMBER,
        po_cur OUT t_cursor
	);

	PROCEDURE CheckValidationFillDestination
    (
        sASSESSMENT_CODE VARCHAR2,
        po_cur OUT t_cursor
    );

	PROCEDURE DeleteByDestination
	(
		sASSESSMENT_CODE VARCHAR,
		sDESTINATION_CODE VARCHAR
	);

END;
/

CREATE OR REPLACE PACKAGE BODY DXDIR_PK_INPUT_DESTINATION AS

	PROCEDURE InsertRecord
	(
		dcINPUT_ID NUMBER,
		sDESTINATION_CODE VARCHAR,
		dcOUTPUT_CATEGORY_ID NUMBER,
		dcPERCENTAGE NUMBER
	)
	IS
	BEGIN
		INSERT INTO DXDIR_INPUT_DESTINATION
		(
			INPUT_ID,
			DESTINATION_CODE,
			OUTPUT_CATEGORY_ID,
			PERCENTAGE
		)
		VALUES
		(
			dcINPUT_ID,
			sDESTINATION_CODE,
			dcOUTPUT_CATEGORY_ID,
			dcPERCENTAGE
		);
	END;

	PROCEDURE UpdateRecord
	(
		dcINPUT_ID NUMBER,
		sDESTINATION_CODE VARCHAR,
		dcOUTPUT_CATEGORY_ID NUMBER,
		dcPERCENTAGE NUMBER
	)
	IS
	BEGIN
		UPDATE DXDIR_INPUT_DESTINATION
		SET
			PERCENTAGE = dcPERCENTAGE
		WHERE
			INPUT_ID = dcINPUT_ID
			AND DESTINATION_CODE = sDESTINATION_CODE
			AND OUTPUT_CATEGORY_ID = dcOUTPUT_CATEGORY_ID;
	END;

	PROCEDURE DeleteRecord
	(
		dcINPUT_ID NUMBER,
		sDESTINATION_CODE VARCHAR,
		dcOUTPUT_CATEGORY_ID NUMBER
	)
	IS
	BEGIN
		DELETE FROM DXDIR_INPUT_DESTINATION
		WHERE
			INPUT_ID = dcINPUT_ID
			AND DESTINATION_CODE = sDESTINATION_CODE
			AND OUTPUT_CATEGORY_ID = dcOUTPUT_CATEGORY_ID;
	END;

    PROCEDURE DeleteDestiantionByCodes
    (
        dcINPUT_ID NUMBER,
		sDESTINATION_CODES DX_PK_UTILITY.DX_Varchar2Array
    )
    IS
		v_destinationCodes DX_Varchar2Table; 
    BEGIN
		v_destinationCodes := DX_PK_Utility.ArrayToTable(sDESTINATION_CODES);

        DELETE FROM DXDIR_INPUT_DESTINATION
        WHERE INPUT_ID = dcINPUT_ID
		  AND DESTINATION_CODE IN (SELECT COLUMN_VALUE FROM THE(SELECT CAST(v_destinationCodes AS DX_Varchar2Table) FROM DUAL));
    END;

	PROCEDURE SelectFoodDestinations
	(
        sASSESSMENT_CODE VARCHAR2,
        dcASSESSMENT_LC_STAGE_ID NUMBER,
        po_cur OUT t_cursor
	)
    IS
    BEGIN
        OPEN po_cur FOR
            WITH cteINPUTS_UNION AS
            (
                SELECT DXDIR_INPUT.ID, 
                       DXDIR_INPUT.ASSESSMENT_CODE, 
                       4 AS OUTPUT_CATEGORY_ID,
                       'FOOD' AS OUTPUT_CATEGORY_TITLE,
                       DXDIR_INPUT.MAT_PLANT,
                       DXDIR_INPUT.MAT_CODE,
                       DXDIR_INPUT.PART_OF_PRODUCT_COPRODUCT,
                       DXDIR_INPUT.CATEGORY_SORT_ORDER,
                       DXDIR_INPUT.INPUT_SORT_ORDER
                FROM DXDIR_INPUT
                    INNER JOIN DXDIR_INPUT_CATEGORY
                    ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
                WHERE DXDIR_INPUT.INEDIBLE_PARTS < 1.0 AND DXDIR_INPUT.ASSESSMENT_CODE = sASSESSMENT_CODE AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
                UNION ALL
                SELECT DXDIR_INPUT.ID,
                       DXDIR_INPUT.ASSESSMENT_CODE,
                       5 AS OUTPUT_CATEGORY_ID,
                       'INEDIBLE_PARTS' AS OUTPUT_CATEGORY_TITLE,
                       DXDIR_INPUT.MAT_PLANT,
                       DXDIR_INPUT.MAT_CODE,
                       0 AS PART_OF_PRODUCT_COPRODUCT,
                       DXDIR_INPUT.CATEGORY_SORT_ORDER,
                       DXDIR_INPUT.INPUT_SORT_ORDER
                FROM DXDIR_INPUT
                    INNER JOIN DXDIR_INPUT_CATEGORY
                    ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
                WHERE DXDIR_INPUT.INEDIBLE_PARTS > 0.0 AND DXDIR_INPUT.ASSESSMENT_CODE = sASSESSMENT_CODE AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
            ),
            cteINPUTS_W_DEST AS
            (
                SELECT cteINPUTS_UNION.ID as INPUT_ID,
                       cteINPUTS_UNION.OUTPUT_CATEGORY_ID,
                       cteINPUTS_UNION.OUTPUT_CATEGORY_TITLE,
                       cteINPUTS_UNION.MAT_PLANT,
                       cteINPUTS_UNION.MAT_CODE,
					   cteINPUTS_UNION.PART_OF_PRODUCT_COPRODUCT,
                       cteINPUTS_UNION.CATEGORY_SORT_ORDER,
                       cteINPUTS_UNION.INPUT_SORT_ORDER,
                       DXDIR_INPUT_DESTINATION.DESTINATION_CODE,
                       DXDIR_INPUT_DESTINATION.PERCENTAGE
                FROM cteINPUTS_UNION
                    LEFT JOIN DXDIR_INPUT_DESTINATION
                    ON DXDIR_INPUT_DESTINATION.INPUT_ID = cteINPUTS_UNION.ID
                    AND DXDIR_INPUT_DESTINATION.OUTPUT_CATEGORY_ID = cteINPUTS_UNION.OUTPUT_CATEGORY_ID
            )
            SELECT INPUT_ID,
                   OUTPUT_CATEGORY_ID,
                   OUTPUT_CATEGORY_TITLE,
                   MAT_PLANT,
                   MAT_CODE,
				   PART_OF_PRODUCT_COPRODUCT,
				   PRODUCT,
				   PRODUCT_2,
				   PRODUCT_3,
				   COPRODUCT,
				   COPRODUCT_2,
                   FOOD_RESCUE,
                   ANIMAL_FEED,
                   BIOMASS_MATERIAL,
                   CODIGESTION_ANAEROBIC,
                   COMPOSTING,
                   COMBUSTION,
                   LAND_APP,
                   LANDFILL,
                   NOT_HARVESTED,
                   REFUSE_DISCARD,
                   SEWER,
                   ENVIRONMENT_LOSS,
                   OTHER
            FROM cteINPUTS_W_DEST
            PIVOT (
                MIN(PERCENTAGE)
                FOR DESTINATION_CODE
                -- below are all possible values from DXDIR_DESTINATION this can only be dynamic with PIVOT XML or DYNAMIC SQL
                IN ('PRODUCT' PRODUCT,
					'PRODUCT_2' PRODUCT_2,
					'PRODUCT_3' PRODUCT_3,
                    'COPRODUCT' COPRODUCT,
					'COPRODUCT_2' COPRODUCT_2,
                    'FOOD_RESCUE' FOOD_RESCUE,
                    'ANIMAL_FEED' ANIMAL_FEED,
                    'BIOMASS_MATERIAL' BIOMASS_MATERIAL,
                    'CODIGESTION_ANAEROBIC' CODIGESTION_ANAEROBIC,
                    'COMPOSTING' COMPOSTING,
                    'COMBUSTION' COMBUSTION,
                    'LAND_APP' LAND_APP,
                    'LANDFILL' LANDFILL,
                    'NOT_HARVESTED' NOT_HARVESTED,
                    'REFUSE_DISCARD' REFUSE_DISCARD,
                    'SEWER' SEWER,
                    'ENVIRONMENT_LOSS' ENVIRONMENT_LOSS,
                    'OTHER' OTHER
                   )
            )
            ORDER BY OUTPUT_CATEGORY_ID, CATEGORY_SORT_ORDER, INPUT_SORT_ORDER ASC;
    END;

	PROCEDURE SelectNonFoodDestinations
	(
        sASSESSMENT_CODE VARCHAR2,
        dcASSESSMENT_LC_STAGE_ID NUMBER,
        po_cur OUT t_cursor
	)
    IS
    BEGIN
        OPEN po_cur FOR
           WITH cteINPUTS_W_DEST AS
			(
				SELECT DXDIR_INPUT.ID as INPUT_ID,
						6 AS OUTPUT_CATEGORY_ID,
						'NON_FOOD' AS OUTPUT_CATEGORY_TITLE,
						DXDIR_INPUT.MAT_PLANT,
						DXDIR_INPUT.MAT_CODE,
						DXDIR_INPUT.PART_OF_PRODUCT_COPRODUCT,
                        DXDIR_INPUT.CATEGORY_SORT_ORDER,
                        DXDIR_INPUT.INPUT_SORT_ORDER,
						DXDIR_INPUT_DESTINATION.DESTINATION_CODE,
						DXDIR_INPUT_DESTINATION.PERCENTAGE
				FROM DXDIR_INPUT
					INNER JOIN DXDIR_INPUT_CATEGORY
					ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
					LEFT JOIN DXDIR_INPUT_DESTINATION
					ON DXDIR_INPUT_DESTINATION.INPUT_ID = DXDIR_INPUT.ID
					AND DXDIR_INPUT_DESTINATION.OUTPUT_CATEGORY_ID = 6
					WHERE DXDIR_INPUT.ASSESSMENT_CODE = sASSESSMENT_CODE AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND NVL(DXDIR_INPUT_CATEGORY.TYPE, 'PACK') <> 'FOOD'
			)
			SELECT INPUT_ID,
				OUTPUT_CATEGORY_ID,
				OUTPUT_CATEGORY_TITLE,
				MAT_PLANT,
				MAT_CODE,
				PART_OF_PRODUCT_COPRODUCT,
				PRODUCT,
				PRODUCT_2,
				PRODUCT_3,
				COPRODUCT,
				COPRODUCT_2,
				FOOD_RESCUE,
				RECYCLING,
				INCIN_WITH_EN_RECOVER,
				LANDFILL,
				SEWER,
				OTHER
			FROM cteINPUTS_W_DEST
			PIVOT (
				MIN(PERCENTAGE)
				FOR DESTINATION_CODE
				-- below are all possible values from DXDIR_DESTINATION this can only be dynamic with PIVOT XML or DYNAMIC SQL
                IN ('PRODUCT' PRODUCT,
					'PRODUCT_2' PRODUCT_2,
					'PRODUCT_3' PRODUCT_3,
                    'COPRODUCT' COPRODUCT,
					'COPRODUCT_2' COPRODUCT_2,
					'FOOD_RESCUE' FOOD_RESCUE,
					'RECYCLING' RECYCLING,
					'INCIN_WITH_EN_RECOVER' INCIN_WITH_EN_RECOVER,
					'LANDFILL' LANDFILL,
					'SEWER' SEWER,
					'OTHER' OTHER
					)
			)
			ORDER BY OUTPUT_CATEGORY_ID, CATEGORY_SORT_ORDER, INPUT_SORT_ORDER ASC;
	END;

    PROCEDURE CheckValidationFillDestination
    (
        sASSESSMENT_CODE VARCHAR2,
        po_cur OUT t_cursor
    )
	IS
    BEGIN
        OPEN po_cur FOR
            with cteOutputCategory AS 
            (
                SELECT DISTINCT DI.ASSESSMENT_LC_STAGE_ID, OUTPUT_CATEGORY_ID
                FROM DXDIR_INPUT DI INNER JOIN DXDIR_INPUT_DESTINATION DD
                    ON DI.ID = DD.INPUT_ID
                WHERE DI.ASSESSMENT_CODE = sASSESSMENT_CODE
                GROUP BY DI.ASSESSMENT_LC_STAGE_ID, DI.ID, OUTPUT_CATEGORY_ID
                HAVING SUM(DD.PERCENTAGE) <> 100
            )
            SELECT ASSESSMENT_LC_STAGE_ID AS LCSTAGE , TITLE AS TITLE, 
                (CASE
                    WHEN SUM(OUTPUT_CATEGORY_ID) = 10 THEN 3
                    WHEN SUM(OUTPUT_CATEGORY_ID) = 6  THEN 2
                    WHEN SUM(OUTPUT_CATEGORY_ID) = 4  THEN 1
                ELSE 0 
                END) AS INVALID
            FROM cteOutputCategory cteO INNER JOIN DXDIR_ASSESSMENT_LC_STAGE DALC
                ON cteO.ASSESSMENT_LC_STAGE_ID = DALC.ID
			WHERE DALC.ASSESSMENT_CODE = sASSESSMENT_CODE
            GROUP BY ASSESSMENT_LC_STAGE_ID, TITLE;
    END;

	PROCEDURE DeleteByDestination
	(
		sASSESSMENT_CODE VARCHAR,
		sDESTINATION_CODE VARCHAR
	)
	IS
	BEGIN
		DELETE FROM DXDIR_INPUT_DESTINATION
		WHERE
			INPUT_ID in ( SELECT ID FROM DXDIR_INPUT WHERE DXDIR_INPUT.ASSESSMENT_CODE = sASSESSMENT_CODE )
			AND DESTINATION_CODE = sDESTINATION_CODE;
	END;

END;
/
