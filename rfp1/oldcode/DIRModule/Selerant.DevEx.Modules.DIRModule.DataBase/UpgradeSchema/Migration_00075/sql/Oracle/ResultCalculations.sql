CREATE OR REPLACE PACKAGE DXDIR_PK_RESULTCALCULATIONS AS

	TYPE t_cursor IS REF CURSOR;

	FUNCTION GetSumMassInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	FUNCTION GetSumMassWaterExcludedInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	FUNCTION GetSumCostInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	FUNCTION GetSumFoodMassInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	FUNCTION GetFoodWaterInputToDestination
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		sDESTINATION_CODE VARCHAR2
	)
	RETURN NUMBER;

	FUNCTION GetFoodIngMassToCoFoodRescInp
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetSumWeightFromOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetSumWeightWaterExOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetSumWeightPackgWasteOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	FUNCTION GetSumWeightFoodPartOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	FUNCTION GetSumWeightEdiblePartOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetCostOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetSumBusinessCost
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetCostElectricity
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetUtilisedInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	FUNCTION GetStockHolding
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetFoodMaterialLoss
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetEdibleFoodMatLoss
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetEdibleMatLossToMatLoss
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetMatLossToInputExWater
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION GetPackgLossToFoodInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	FUNCTION GetPackToGrossProduct
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	PROCEDURE WasteCollectionTreatmentProc
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcRESULT OUT NUMBER
	);

	FUNCTION WasteCollectionTreatment
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

    FUNCTION RevenueFromOutputIncome
    (
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	PROCEDURE EstimatedCostOfWaste
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcRESULT OUT NUMBER
	);
	
	FUNCTION GetEnergyCost
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	FUNCTION GetMaterialSpend
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;
	
	FUNCTION DisposalToPurchase
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER;

	FUNCTION EstimatedWasteToPurchase
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		estimCostOfWaste NUMBER
	)
	RETURN NUMBER;

	PROCEDURE GetResultsTable
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		po_cur  IN OUT t_cursor
	);

	PROCEDURE GetFoodLossesNotIncIndblParts
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		po_cur OUT t_cursor
	);

	PROCEDURE GetFoodLossesInediblePartsOnly
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		po_cur OUT t_cursor
	);

END;
/

CREATE OR REPLACE PACKAGE BODY DXDIR_PK_RESULTCALCULATIONS AS 

	FUNCTION GetSumMassInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(25, 3) := 0;
	BEGIN

		SELECT NVL(SUM(MASS), 0)
		INTO result
		FROM DXDIR_INPUT 
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID;

	RETURN result;
	END;

	FUNCTION GetSumMassWaterExcludedInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(25, 3) := 0;
	BEGIN

		SELECT NVL(SUM(MASS), 0)
		INTO result
		FROM DXDIR_INPUT 
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND
			  MAT_CODE				 NOT IN ('DIR_FOOD_WATER', 'DIR_NONFOOD_WATER');

	RETURN result;
	END;

	FUNCTION GetSumCostInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(25, 4) := 0;
	BEGIN
	
		SELECT NVL(SUM(COST), 0)
		INTO result
		FROM DXDIR_INPUT 
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID;

	RETURN result;
	END;

	FUNCTION GetSumFoodMassInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(30, 3) := 0;
	BEGIN
	
		SELECT NVL(SUM(MASS), 0)
		INTO result
		FROM DXDIR_INPUT 
			 INNER JOIN DXDIR_INPUT_CATEGORY
			 ON DXDIR_INPUT.INPUT_CATEGORY_ID	 = DXDIR_INPUT_CATEGORY.ID
		WHERE DXDIR_INPUT.ASSESSMENT_CODE		 = sASSESSMENT_CODE AND
			  DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND
			  DXDIR_INPUT_CATEGORY.TYPE			 = 'FOOD';

	RETURN result;
	END;

	FUNCTION GetFoodWaterInputToDestination
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		sDESTINATION_CODE VARCHAR2
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
	BEGIN
	
		SELECT NVL(SUM(MASS * PERCENTAGE / 100), 0)
		INTO result
		FROM DXDIR_INPUT
			 INNER JOIN DXDIR_INPUT_DESTINATION 
			 ON DXDIR_INPUT.ID					 = DXDIR_INPUT_DESTINATION.INPUT_ID
			 INNER JOIN DXDIR_INPUT_CATEGORY 
			 ON DXDIR_INPUT.INPUT_CATEGORY_ID	 = DXDIR_INPUT_CATEGORY.ID
		WHERE DXDIR_INPUT.ASSESSMENT_CODE				 = sASSESSMENT_CODE AND
			 DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID			 = dcASSESSMENT_LC_STAGE_ID AND
			 DXDIR_INPUT.MAT_CODE						 = 'DIR_FOOD_WATER' AND 
			 DXDIR_INPUT_CATEGORY.TYPE					 = 'FOOD'AND 
			 DXDIR_INPUT_DESTINATION.DESTINATION_CODE	 = sDESTINATION_CODE;

	RETURN result;
	END;


	FUNCTION GetFoodIngMassToCoFoodRescInp
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
	BEGIN
	
		SELECT NVL(SUM(MASS * (1 - DXDIR_INPUT.INEDIBLE_PARTS) * DXDIR_INPUT_DESTINATION.PERCENTAGE/100), 0)
		INTO result
		FROM DXDIR_INPUT
			 INNER JOIN DXDIR_INPUT_DESTINATION 
			 ON DXDIR_INPUT_DESTINATION.INPUT_ID	 = DXDIR_INPUT.ID
			 INNER JOIN DXDIR_INPUT_CATEGORY 
			 ON DXDIR_INPUT.INPUT_CATEGORY_ID		 = DXDIR_INPUT_CATEGORY.ID 
		WHERE DXDIR_INPUT_CATEGORY.TYPE					 = 'FOOD'AND 
			  DXDIR_INPUT.ASSESSMENT_CODE				 = sASSESSMENT_CODE AND 
			  DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID		 = dcASSESSMENT_LC_STAGE_ID AND 
			  DXDIR_INPUT_DESTINATION.DESTINATION_CODE	 IN ('COPRODUCT','FOOD_RESCUE');

	RETURN result;
	END;


	FUNCTION GetSumWeightFromOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(25, 3) := 0;
	BEGIN
	
		SELECT NVL(SUM(WEIGHT), 0)
		INTO result
		FROM DXDIR_OUTPUT 
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
			  (INPUT_ID IS NOT NULL OR (INPUT_ID IS NULL AND DESTINATION_CODE IS NULL AND OUTPUT_CATEGORY_ID <> 1));

	RETURN result;
	END;
	
	FUNCTION GetSumWeightWaterExOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		sumWeight1 NUMBER(26, 12);
		sumWeight2 NUMBER(26, 12);
	BEGIN
		SELECT NVL(SUM(WEIGHT), 0)
		INTO sumWeight1
		FROM DXDIR_OUTPUT 
			 INNER JOIN DXDIR_INPUT 
			 ON DXDIR_OUTPUT.INPUT_ID = DXDIR_INPUT.ID 
		WHERE DXDIR_OUTPUT.ASSESSMENT_CODE			 = sASSESSMENT_CODE AND 
			  DXDIR_OUTPUT.ASSESSMENT_LC_STAGE_ID	 = dcASSESSMENT_LC_STAGE_ID AND 
			  INPUT_ID								 IS NOT NULL AND 
			  DXDIR_INPUT.MAT_CODE					 NOT IN ('DIR_FOOD_WATER', 'DIR_NONFOOD_WATER');

		SELECT NVL(SUM(WEIGHT), 0)
		INTO sumWeight2
		FROM DXDIR_OUTPUT 
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
			  INPUT_ID				 IS NULL AND 
			  DESTINATION_CODE		 IS NULL AND 
			  OUTPUT_CATEGORY_ID	 <> 1;

		RETURN sumWeight1 + sumWeight2;
	END;
	
	FUNCTION GetSumWeightPackgWasteOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
	BEGIN

		SELECT NVL(SUM(DXDIR_OUTPUT.WEIGHT), 0)
		INTO result
		FROM  DXDIR_INPUT 
			 INNER JOIN DXDIR_OUTPUT 
			 ON DXDIR_INPUT.ID = DXDIR_OUTPUT.INPUT_ID
				 INNER JOIN DXDIR_INPUT_CATEGORY 
				 ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
		WHERE DXDIR_OUTPUT.ASSESSMENT_CODE			 = sASSESSMENT_CODE AND 
			  DXDIR_OUTPUT.ASSESSMENT_LC_STAGE_ID	 = dcASSESSMENT_LC_STAGE_ID AND
			  DXDIR_OUTPUT.OUTPUT_CATEGORY_ID		 = 6 AND 
			  DXDIR_OUTPUT.INPUT_ID					 IS NOT NULL AND
			  DXDIR_INPUT_CATEGORY.TYPE				 <> 'FOOD'  AND
			  DXDIR_INPUT.PACKAGING                  = 1;

		RETURN result;
	END;
	
	FUNCTION GetSumWeightFoodPartOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(25, 3) := 0;
	BEGIN

		SELECT NVL(SUM(DXDIR_OUTPUT.WEIGHT), 0)
		INTO result
		FROM DXDIR_OUTPUT 
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND
			  INPUT_ID IS NOT NULL AND OUTPUT_CATEGORY_ID IN  (4, 5);

		RETURN result;
	END;


	FUNCTION GetSumWeightEdiblePartOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(25, 3) := 0;
	BEGIN

		SELECT NVL(SUM(DXDIR_OUTPUT.WEIGHT), 0)
		INTO result
		FROM DXDIR_OUTPUT 
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
			  INPUT_ID				 IS NOT NULL AND 
			  OUTPUT_CATEGORY_ID	 = 4;

		RETURN result;
	END;


	FUNCTION GetCostOutput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 4) := 0;
	BEGIN

		SELECT NVL(SUM(COST), 0)
		INTO result
		FROM DXDIR_OUTPUT
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
			  (INPUT_ID IS NOT NULL OR (INPUT_ID IS NULL AND DESTINATION_CODE IS NULL AND OUTPUT_CATEGORY_ID <> 1));

		RETURN result;
	END;

	FUNCTION GetSumBusinessCost
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(25, 4) := 0;
	BEGIN

		SELECT NVL(SUM(COST), 0)
		INTO result
		FROM DXDIR_BUSINESS_COST 
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID;

		RETURN result;
	END;


	FUNCTION GetCostElectricity
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(30, 4) := 0;
	BEGIN

		SELECT NVL(SUM(COST), 0)
		INTO result
		FROM DXDIR_BUSINESS_COST 
		WHERE ASSESSMENT_CODE			 = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID	 = dcASSESSMENT_LC_STAGE_ID AND
			  DXDIR_BUSINESS_COST.TITLE	 IN ('ELECTRICITY', 'GAS');

		RETURN result;
	END;

	/*Row for results grid*/
	/*Utilised input to gross product ratio*/
	FUNCTION GetUtilisedInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
		sumWeight NUMBER(26, 12) := 0;
		sumMass NUMBER(26, 12) := 0;
	BEGIN
		sumMass := GetSumMassInput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		IF sumMass = 0
		THEN
			RETURN result;
		END IF;

		sumWeight := GetSumWeightFromOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		result := 1 - sumWeight / sumMass;
		RETURN result;
	END;

	/*Stock holding to gross product ratio*/
	FUNCTION GetStockHolding
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
		sumWeight NUMBER := 0;
		sumMass NUMBER := 0;
	BEGIN
		sumMass := GetSumMassInput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		IF sumMass = 0 
		THEN
			RETURN result;
		END IF;

		SELECT NVL(SUM(WEIGHT),0)
		INTO sumWeight
		FROM DXDIR_OUTPUT 
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
			  (INPUT_ID IS NULL AND DESTINATION_CODE IS NULL AND OUTPUT_CATEGORY_ID = 1) ;

		result := 1 - sumWeight / sumMass;
		RETURN result;
	END;

	/*Food material loss (with inedible parts) to total material loss ratio*/
	FUNCTION GetFoodMaterialLoss
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
		sumWeight NUMBER := 0;
		sumMass NUMBER := 0;
		sumWeightWasteFood NUMBER := 0;
	BEGIN
		sumWeight := GetSumWeightFromOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		IF sumWeight = 0
		THEN
			RETURN result;
		END IF;

		sumWeightWasteFood := GetSumWeightFoodPartOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
		sumMass := GetFoodIngMassToCoFoodRescInp(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		result := (sumMass + sumWeightWasteFood) / sumWeight;
		RETURN result;
	END;

	/*Food material loss (without inedible parts) to inputs ratio*/
	FUNCTION GetEdibleFoodMatLoss
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
		sumFoodMassCo NUMBER := 0;
		sumMass NUMBER := 0;
		sumWeightEdiblePart NUMBER := 0;
	BEGIN
		sumMass := GetSumMassInput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		IF sumMass = 0
		THEN
			RETURN result;
		END IF;

		sumFoodMassCo := GetFoodIngMassToCoFoodRescInp(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
		sumWeightEdiblePart := GetSumWeightEdiblePartOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		result := (sumWeightEdiblePart + sumFoodMassCo) / sumMass;
		RETURN result;
	END;

	/*Food material loss (without inedible parts) to total material loss ratio */
	FUNCTION GetEdibleMatLossToMatLoss
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
		sumFoodMassCo NUMBER := 0;
		sumWeight NUMBER := 0;
		sumWeightEdiblePart NUMBER := 0;
	BEGIN
		sumWeight := GetSumWeightFromOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		IF sumWeight = 0
		THEN
			RETURN result;
		END IF;

		sumFoodMassCo := GetFoodIngMassToCoFoodRescInp(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
		sumWeightEdiblePart := GetSumWeightEdiblePartOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		result := (sumWeightEdiblePart + sumFoodMassCo) / sumWeight;
		RETURN result;
	END;

	/*Total material loss (ex water) to material input (ex water) ratio*/
	FUNCTION GetMatLossToInputExWater
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
		sumMassWaterEx NUMBER := 0;
		sumWeightExWater NUMBER := 0;
		waterInputCoProduct NUMBER := 0;
		waterInputFoodRescue NUMBER := 0;
	BEGIN
		sumMassWaterEx := GetSumMassWaterExcludedInput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		IF sumMassWaterEx = 0
		THEN
			RETURN result;
		END IF;

		sumWeightExWater := GetSumWeightWaterExOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
		waterInputCoProduct := GetFoodWaterInputToDestination(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID, 'COPRODUCT');
		waterInputFoodRescue := GetFoodWaterInputToDestination(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID, 'FOOD_RESCUE');

		result := (sumWeightExWater - waterInputCoProduct - waterInputFoodRescue) / sumMassWaterEx;
		RETURN result;
	END;

	/*Packaging material loss to food input ratio*/
	FUNCTION GetPackgLossToFoodInput
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
		sumMassFood NUMBER(26, 12) := 0;
		sumWeightPack NUMBER(26, 12) := 0;
	BEGIN
		sumMassFood := GetSumFoodMassInput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		IF sumMassFood = 0
		THEN
			RETURN result;
		END IF;

		sumWeightPack := GetSumWeightPackgWasteOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		result := sumWeightPack / sumMassFood;
		RETURN result;
	END;

	/*Output product packaging to gross product ratio*/
	FUNCTION GetPackToGrossProduct
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
		packNonFoodProductSum NUMBER := 0;
		productOutputWeight NUMBER := 0;
	BEGIN

		SELECT NVL(WEIGHT, 0) 
		INTO productOutputWeight
		FROM DXDIR_OUTPUT
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND
			  OUTPUT_CATEGORY_ID	 = 1;

		IF productOutputWeight = 0
		THEN
			RETURN result;
		END IF;

		SELECT NVL(SUM(DXDIR_INPUT.MASS * DXDIR_INPUT_DESTINATION.PERCENTAGE / 100), 0)
		INTO packNonFoodProductSum
		FROM DXDIR_INPUT  
			INNER JOIN DXDIR_INPUT_CATEGORY
			ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
				INNER JOIN DXDIR_INPUT_DESTINATION 
				ON DXDIR_INPUT.ID = DXDIR_INPUT_DESTINATION.INPUT_ID
		WHERE DXDIR_INPUT.ASSESSMENT_CODE				 = sASSESSMENT_CODE AND 
			  DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID		 = dcASSESSMENT_LC_STAGE_ID AND 
			  DXDIR_INPUT_CATEGORY.TYPE					 <> 'FOOD' AND 
			  DXDIR_INPUT.PACKAGING						 = 1 AND
			  DXDIR_INPUT_DESTINATION.DESTINATION_CODE	 = 'PRODUCT';

		result := packNonFoodProductSum / productOutputWeight;
		RETURN result;
	END;

	PROCEDURE WasteCollectionTreatmentProc
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcRESULT OUT NUMBER
	)
	IS
	BEGIN
		dcRESULT := WasteCollectionTreatment(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
	END;

	/* Waste removal cost */
	FUNCTION WasteCollectionTreatment
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
	BEGIN
	
		SELECT NVL(SUM(OUTPUT_COST), 0)
		INTO result
		FROM
		(
			SELECT OUTPUT_COST FROM DXDIR_OUTPUT
			WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
				  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
				  DESTINATION_CODE		 IS NOT NULL AND 
				  INPUT_ID				 IS NULL
			UNION ALL
			SELECT OUTPUT_COST FROM DXDIR_OUTPUT
			WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
				  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
				  OUTPUT_CATEGORY_ID IN (2, 3)
		);

		RETURN result;
	END;

    FUNCTION RevenueFromOutputIncome
    (
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
    IS
        result NUMBER(26, 7) := 0;
        productValuePerMass NUMBER(26, 12);
    BEGIN
        productValuePerMass := DXDIR_PK_CALCULATIONS.GetValuePerMass(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID, 1);
    
        IF (productValuePerMass <> 0)
        THEN
            SELECT NVL(SUM(SUM(COST) * SUM(INCOME) / SUM(WEIGHT) / productValuePerMass), 0)
            INTO result
            FROM DXDIR_OUTPUT
            WHERE ASSESSMENT_CODE = sASSESSMENT_CODE
              AND ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID
              AND OUTPUT_CATEGORY_ID in (4,5,6)
                GROUP BY OUTPUT_CATEGORY_ID, DESTINATION_CODE
                HAVING SUM(INCOME) > 0 AND SUM(WEIGHT) > 0;
        END IF;

        RETURN result;    
    END;

	/* Estimated true cost of waste*/
	PROCEDURE EstimatedCostOfWaste
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcRESULT OUT NUMBER 
	)
	IS
		coProductMargin NUMBER(26, 12);
		foodRescueMargin NUMBER(26, 12);
		sumCostRelatedWaste  NUMBER(26, 12) := 0;
		costCoproduct NUMBER(26, 12);
		costFoodRescue NUMBER(26, 12);

		wasteCost  NUMBER(26, 4) := 0;
		sumCostOutput  NUMBER(26, 4) := 0;
		wasteCollectionTreat NUMBER(26, 4) := 0;
        revenueFromIncome NUMBER(26, 12) := 0;

	    TYPE r_BussinessCostGridItem IS RECORD
		(
			ID					   DXDIR_BUSINESS_COST.ID%TYPE,
			ASSESSMENT_CODE		   DXDIR_BUSINESS_COST.ASSESSMENT_CODE%TYPE,
			ASSESSMENT_LC_STAGE_ID DXDIR_BUSINESS_COST.ASSESSMENT_LC_STAGE_ID%TYPE,
			TYPE				   DXDIR_BUSINESS_COST.TYPE%TYPE,
			TITLE				   DXDIR_BUSINESS_COST.TITLE%TYPE,
			SORT_ORDER			   DXDIR_BUSINESS_COST.SORT_ORDER%TYPE,
			COST				   DXDIR_BUSINESS_COST.COST%TYPE,
			WASTE_COST			   NUMBER(26, 4)
		);

		bussinessCostGridItem r_BussinessCostGridItem;
		PO_CUR DXDIR_PK_BUSINESS_COST.t_cursor;
	BEGIN
		DXDIR_PK_CALCULATIONS.GetMargin(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID, 2, coProductMargin);
		DXDIR_PK_CALCULATIONS.GetMargin(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID, 3, foodRescueMargin);

		SELECT NVL(SUM(COST), 0) 
		INTO costCoproduct 
		FROM DXDIR_OUTPUT
		WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
			 (INPUT_ID IS NULL AND DESTINATION_CODE IS NULL AND OUTPUT_CATEGORY_ID = 2);

		SELECT NVL(SUM(COST), 0) 
		INTO costFoodRescue
		FROM DXDIR_OUTPUT
		WHERE ASSESSMENT_CODE = sASSESSMENT_CODE AND 
			  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
			  (INPUT_ID IS NULL AND DESTINATION_CODE IS NULL AND OUTPUT_CATEGORY_ID = 3);
		
		sumCostRelatedWaste	 := costCoproduct * coProductMargin + costFoodRescue * foodRescueMargin;
		sumCostOutput		 := GetCostOutput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
		wasteCollectionTreat := WasteCollectionTreatment(SASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
        revenueFromIncome    := RevenueFromOutputIncome(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
        
		DXDIR_PK_BUSINESS_COST.GetBusinessCostGridItems(SASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID, PO_CUR);

		LOOP
			FETCH PO_CUR INTO bussinessCostGridItem;
			EXIT WHEN PO_CUR%NOTFOUND;

			wasteCost := wasteCost + NVL(bussinessCostGridItem.WASTE_COST, 0);
		END LOOP;

		dcRESULT := wasteCost + wasteCollectionTreat - sumCostRelatedWaste - revenueFromIncome + sumCostOutput;
	END;

	
	/* Energy spend as % of operating costs*/
	FUNCTION GetEnergyCost
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER (24, 12) := 0;
		sumEnergy NUMBER (24, 12) := 0;
		sumBusinessCost NUMBER (24, 12) := 0;
		sumWaste NUMBER (24, 12) := 0;
		sumCostInput NUMBER (24, 12)  := 0;
	BEGIN
		sumEnergy		 := GetCostElectricity(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
		sumBusinessCost	 := GetSumBusinessCost(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
		sumWaste		 := WasteCollectionTreatment(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
		sumCostInput	 := GetSumCostInput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		IF (sumBusinessCost <> 0 OR sumWaste <> 0 OR sumCostInput <> 0)
		THEN
			result := sumEnergy / (sumWaste + sumBusinessCost + sumCostInput);
		END IF;

		RETURN result;
	END;

	/* Material spend as % of operating cost*/
	FUNCTION GetMaterialSpend
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
		sumWaste NUMBER(26, 12) := 0;
		sumCostInput NUMBER(26, 12) := 0;
		sumBusinessCost NUMBER(26, 12) := 0;
	BEGIN
		sumWaste			 := WasteCollectionTreatment(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
		sumCostInput		 := GetSumCostInput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);
		sumBusinessCost		 := GetSumBusinessCost(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		IF (sumWaste <> 0 OR sumBusinessCost <> 0 OR sumCostInput <> 0)
		THEN
			result := sumCostInput / (sumWaste + sumBusinessCost + sumCostInput);
		END IF;

		RETURN result;
	END;
	
	/*Disposal To Purchase Ratio*/
	FUNCTION DisposalToPurchase
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER(26, 12) := 0;
		sumCostInput NUMBER := 0;
		wasteColltreatment NUMBER := 0;
	BEGIN
		sumCostInput := GetSumCostInput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		IF sumCostInput = 0
		THEN
			RETURN result;
		END IF;

		wasteColltreatment := WasteCollectionTreatment(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		result := wasteColltreatment / sumCostInput;
		RETURN result;
	END;

	/*Estimated true cost of waste ratio to purchase ratio*/
	FUNCTION EstimatedWasteToPurchase
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		estimCostOfWaste NUMBER
	)
	RETURN NUMBER
	IS
		result NUMBER (26, 12) := 0;
		sumCostInput NUMBER (26, 12);
	BEGIN

		sumCostInput := GetSumCostInput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID);

		IF sumCostInput <> 0
		THEN
			result := estimCostOfWaste / sumCostInput;
		END IF;
		
		RETURN result;
	END;


	PROCEDURE GetResultsTable
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		po_cur IN OUT t_cursor
	)
	IS
		estimCostOfWaste NUMBER (26, 12) := 0;
		v_cursor t_cursor;
	BEGIN
		DXDIR_PK_RESULTCALCULATIONS.EstimatedCostOfWaste(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID, estimCostOfWaste);

		BEGIN
		OPEN v_cursor FOR
			WITH MYTABLE AS
			(
                SELECT 1 AS ROW_ID,
                       GetUtilisedInput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) * 100 AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 2 AS ROW_ID,
                       GetStockHolding(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) * 100 AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 3 AS ROW_ID,
                       GetFoodMaterialLoss(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) * 100 AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 4 AS ROW_ID,
                       GetEdibleFoodMatLoss(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) * 100 AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 5 AS ROW_ID,
                       GetEdibleMatLossToMatLoss(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) * 100 AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 6 AS ROW_ID,
                       GetMatLossToInputExWater(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) * 100 AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 7 AS ROW_ID,
                       GetPackgLossToFoodInput(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) * 100 AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 8 AS ROW_ID,
                       GetPackToGrossProduct(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) * 100 AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 9 AS ROW_ID,
                       estimCostOfWaste AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 10 AS ROW_ID,
                       WasteCollectionTreatment(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 11 AS ROW_ID,
                       GetEnergyCost(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) * 100 AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 12 AS ROW_ID,
                       GetMaterialSpend(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) * 100 AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 13 AS ROW_ID,
                       DisposalToPurchase(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID) * 100 AS RESULT
                FROM DUAL
                UNION ALL
                SELECT 14 AS ROW_ID,
                      EstimatedWasteToPurchase(sASSESSMENT_CODE, dcASSESSMENT_LC_STAGE_ID, estimCostOfWaste) * 100 AS RESULT
                FROM DUAL
			)
			SELECT DXDIR_RESULT_ROW.ID,
				   DXDIR_RESULT_ROW.TITLE,
				   DXDIR_RESULT_ROW.RESULT_UOM,
				   DXDIR_RESULT_ROW.RESULT_TYPE,
				   DXDIR_RESULT_ROW.SORT_ORDER, RESULT
			FROM MYTABLE
				 INNER JOIN DXDIR_RESULT_ROW
				 ON MYTABLE.ROW_ID = DXDIR_RESULT_ROW.ID;
		EXCEPTION
			WHEN NO_DATA_FOUND THEN
            RETURN;
		END;

		po_cur := v_cursor;
	END;

	PROCEDURE GetFoodLossesNotIncIndblParts
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER, 
		po_cur OUT t_cursor
	)
	IS
	BEGIN
	
	OPEN po_cur FOR 
	SELECT NVL(WEIGHT, 0) AS WEIGHT,
		   OC.TYPE AS CODE,
		   0 AS WASTE,
		   0 AS SORT_ORDER,
		   0 AS USED_ON,
		   OC.TITLE
	FROM DXDIR_OUTPUT O
		 JOIN DXDIR_OUTPUT_CATEGORY OC
		 ON OC.ID = O.OUTPUT_CATEGORY_ID
	WHERE ASSESSMENT_CODE		 = sASSESSMENT_CODE AND 
		  ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND 
		  OUTPUT_CATEGORY_ID	 IN (2, 3)
	UNION 
	SELECT NVL(SUM(WEIGHT), 0) AS WEIGHT,
		   D.CODE AS CODE,
		   0 AS WASTE,
		   0 AS SORT_ORDER,
		   0 AS USED_ON,
		   D.TITLE
	FROM DXDIR_OUTPUT O
		 JOIN DXDIR_DESTINATION D
		 ON O.DESTINATION_CODE	 = D.CODE
		 JOIN DXDIR_OUTPUT_CATEGORY OC
		 ON OC.ID				 = O.OUTPUT_CATEGORY_ID
	WHERE ASSESSMENT_CODE			 = sASSESSMENT_CODE AND
		  ASSESSMENT_LC_STAGE_ID	 = dcASSESSMENT_LC_STAGE_ID AND 
		  OC.TYPE					 = 'FOOD'
	GROUP BY D.CODE, D.TITLE;

	END;

	PROCEDURE GetFoodLossesInediblePartsOnly
	(
		sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		po_cur OUT t_cursor
	)
	IS
	BEGIN
		OPEN po_cur FOR
		SELECT NVL(SUM(O.WEIGHT), 0) AS WEIGHT,
			   D.CODE AS CODE,
			   0 AS WASTE,
			   0 AS SORT_ORDER,
			   0 AS USED_ON,
			   D.TITLE
		FROM DXDIR_OUTPUT O
			 JOIN DXDIR_DESTINATION D
				ON O.DESTINATION_CODE = D.CODE
			 JOIN DXDIR_OUTPUT_CATEGORY OC
				ON OC.ID = O.OUTPUT_CATEGORY_ID
		WHERE O.ASSESSMENT_CODE			 = sASSESSMENT_CODE			AND
			  O.ASSESSMENT_LC_STAGE_ID	 = dcASSESSMENT_LC_STAGE_ID AND 
			  O.OUTPUT_CATEGORY_ID		 = 5
		GROUP BY D.CODE, D.TITLE;
	END;
END;
/