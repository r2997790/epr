CREATE OR REPLACE PACKAGE DXDIR_PK_OUTPUT AS

	PROCEDURE InsertRecord
	(
		dcID NUMBER,
		sASSESSMENT_CODE VARCHAR2,
		dcOUTPUT_CATEGORY_ID NUMBER,
		sDESTINATION_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcINPUT_ID NUMBER,
		sMAT_PLANT VARCHAR2,
		sMAT_CODE VARCHAR2,
		dcOUTPUT_COST NUMBER,
		dcCOST NUMBER,
		dcINCOME NUMBER,
		dcWEIGHT NUMBER,
		dcSORT_ORDER NUMBER
	);

	PROCEDURE UpdateRecord
	(
		dcID NUMBER,
		sASSESSMENT_CODE VARCHAR2,
		dcOUTPUT_CATEGORY_ID NUMBER,
		sDESTINATION_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcINPUT_ID NUMBER,
		sMAT_PLANT VARCHAR2,
		sMAT_CODE VARCHAR2,
		dcOUTPUT_COST NUMBER,
		dcCOST NUMBER,
		dcINCOME NUMBER,
		dcWEIGHT NUMBER,
		dcSORT_ORDER NUMBER
	);

	PROCEDURE DeleteRecord
	(
		dcID NUMBER
	);

	PROCEDURE GetNextId
	(
        dcNextID OUT NUMBER
    );

    PROCEDURE InsertOrUpdateNonWaste
    (
        sASSESSMENT_CODE VARCHAR2,
        sDESTINATION_CODE VARCHAR,
        dcASSESSMENT_LC_STAGE_ID NUMBER
    );
    
    PROCEDURE ManageWastewaterRow
    (
        sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
    );
END;
/

CREATE OR REPLACE PACKAGE BODY DXDIR_PK_OUTPUT AS

	PROCEDURE InsertRecord
	(
		dcID NUMBER,
		sASSESSMENT_CODE VARCHAR2,
		dcOUTPUT_CATEGORY_ID NUMBER,
		sDESTINATION_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcINPUT_ID NUMBER,
		sMAT_PLANT VARCHAR2,
		sMAT_CODE VARCHAR2,
		dcOUTPUT_COST NUMBER,
		dcCOST NUMBER,
		dcINCOME NUMBER,
		dcWEIGHT NUMBER,
		dcSORT_ORDER NUMBER
	)
	IS
	BEGIN
		INSERT INTO DXDIR_OUTPUT
		(
			ID,
			ASSESSMENT_CODE,
			OUTPUT_CATEGORY_ID,
			DESTINATION_CODE,
			ASSESSMENT_LC_STAGE_ID,
			INPUT_ID,
			MAT_PLANT,
			MAT_CODE,
			OUTPUT_COST,
			COST,
			INCOME,
			WEIGHT,
			SORT_ORDER
		)
		VALUES
		(
			dcID,
			sASSESSMENT_CODE,
			dcOUTPUT_CATEGORY_ID,
			sDESTINATION_CODE,
			dcASSESSMENT_LC_STAGE_ID,
			dcINPUT_ID,
			sMAT_PLANT,
			sMAT_CODE,
			dcOUTPUT_COST,
			dcCOST,
			dcINCOME,
			dcWEIGHT,
			dcSORT_ORDER
		);
	END;

	PROCEDURE UpdateRecord
	(
		dcID NUMBER,
		sASSESSMENT_CODE VARCHAR2,
		dcOUTPUT_CATEGORY_ID NUMBER,
		sDESTINATION_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER,
		dcINPUT_ID NUMBER,
		sMAT_PLANT VARCHAR2,
		sMAT_CODE VARCHAR2,
		dcOUTPUT_COST NUMBER,
		dcCOST NUMBER,
		dcINCOME NUMBER,
		dcWEIGHT NUMBER,
		dcSORT_ORDER NUMBER
	)
	IS
	BEGIN
		UPDATE DXDIR_OUTPUT
		SET 
			ASSESSMENT_CODE = sASSESSMENT_CODE,
			OUTPUT_CATEGORY_ID = dcOUTPUT_CATEGORY_ID,
			DESTINATION_CODE = sDESTINATION_CODE,
			ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID,
			INPUT_ID = dcINPUT_ID,
			MAT_PLANT = sMAT_PLANT,
			MAT_CODE = sMAT_CODE,
			OUTPUT_COST = dcOUTPUT_COST,
			COST = dcCOST,
			INCOME = dcINCOME,
			WEIGHT = dcWEIGHT,
			SORT_ORDER = dcSORT_ORDER
		WHERE
			ID = dcID;
	END;

	PROCEDURE DeleteRecord
	(
		dcID NUMBER
	)
	IS
	BEGIN
		DELETE FROM DXDIR_OUTPUT
		WHERE
			ID = dcID;
	END;

	PROCEDURE GetNextId
	(
        dcNextID OUT NUMBER
    )
	IS
	BEGIN
		SELECT DXDIR_OUTPUT_SEQ.NEXTVAL INTO dcNextID FROM DUAL;
    END;

    PROCEDURE InsertOrUpdateNonWaste
    (
        sASSESSMENT_CODE VARCHAR2,
        sDESTINATION_CODE VARCHAR,
        dcASSESSMENT_LC_STAGE_ID NUMBER
    )
	IS
        outputCategoryId DXDIR_OUTPUT.OUTPUT_CATEGORY_ID%TYPE := NULL;
		destinationSortOrder DXDIR_DESTINATION.SORT_ORDER%TYPE := 0;
		v_removeOutputRow NUMBER;
	BEGIN
        -- OUTPUT_CATEGORY_ID (1, 2 or 3)
        SELECT CASE
                WHEN sDESTINATION_CODE LIKE 'PRODUCT%'
                    THEN 1
                WHEN sDESTINATION_CODE LIKE 'COPRODUCT%'
                    THEN 2
                WHEN sDESTINATION_CODE = 'FOOD_RESCUE'
                    THEN 3
                ELSE
                    NULL
                END as OUTPUT_CATEGORY_ID
        INTO outputCategoryId
        FROM DUAL;
        
		IF (outputCategoryId IS NOT NULL)
		THEN
			
			SELECT SORT_ORDER
			INTO destinationSortOrder
			FROM DXDIR_DESTINATION
			WHERE CODE = sDESTINATION_CODE;
        
			-- sum values and update/insert taget        
			MERGE INTO DXDIR_OUTPUT T
			USING (SELECT SUM((DXDIR_INPUT_DESTINATION.PERCENTAGE / 100.0) * DXDIR_INPUT.COST *
						   (CASE
							  WHEN DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
								THEN 1 - DXDIR_INPUT.INEDIBLE_PARTS
							  ELSE
								  1
							END)) as COST_SUM,
						SUM((DXDIR_INPUT_DESTINATION.PERCENTAGE / 100.0) * DXDIR_INPUT.MASS *
							(CASE
							  WHEN DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
								THEN 1 - DXDIR_INPUT.INEDIBLE_PARTS
							  ELSE
								  1
							END)) as MASS_SUM
					FROM DXDIR_INPUT
					INNER JOIN DXDIR_INPUT_CATEGORY
					ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
					INNER JOIN DXDIR_INPUT_DESTINATION
					ON DXDIR_INPUT.ID = DXDIR_INPUT_DESTINATION.INPUT_ID
					WHERE DXDIR_INPUT_DESTINATION.DESTINATION_CODE = sDESTINATION_CODE AND
						  DXDIR_INPUT.ASSESSMENT_CODE = sASSESSMENT_CODE AND
						  DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID
				) S
			ON (T.ASSESSMENT_CODE = sASSESSMENT_CODE AND
				T.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND
				T.OUTPUT_CATEGORY_ID = outputCategoryId AND
                T.DESTINATION_CODE = sDESTINATION_CODE)
			WHEN MATCHED THEN 
				UPDATE
				SET COST = S.COST_SUM,
					WEIGHT = S.MASS_SUM
			WHEN NOT MATCHED THEN
				INSERT (ID,
						ASSESSMENT_CODE,
						OUTPUT_CATEGORY_ID,
						DESTINATION_CODE,
						ASSESSMENT_LC_STAGE_ID,
						COST,
						WEIGHT,
						SORT_ORDER)
				VALUES (DXDIR_OUTPUT_SEQ.NEXTVAL,
						sASSESSMENT_CODE,
						outputCategoryId,
						sDESTINATION_CODE,
						dcASSESSMENT_LC_STAGE_ID,
						S.COST_SUM,
						S.MASS_SUM,
						destinationSortOrder);  

			BEGIN
				SELECT CASE 
					WHEN NVL(COST, 0) = 0 AND NVL(WEIGHT, 0) = 0 
					THEN 1 
					ELSE 0 END
				INTO v_removeOutputRow
				FROM DXDIR_OUTPUT
				WHERE ASSESSMENT_CODE		   = sASSESSMENT_CODE				AND
					  ASSESSMENT_LC_STAGE_ID   = dcASSESSMENT_LC_STAGE_ID		AND
					  OUTPUT_CATEGORY_ID	   = outputCategoryId				AND
					  DESTINATION_CODE         = sDESTINATION_CODE;
				EXCEPTION
					WHEN NO_DATA_FOUND THEN
						v_removeOutputRow := 0;
			END;

			IF (v_removeOutputRow = 1)
			THEN
				DELETE FROM DXDIR_OUTPUT
				WHERE ASSESSMENT_CODE		   = sASSESSMENT_CODE			AND
					  ASSESSMENT_LC_STAGE_ID   = dcASSESSMENT_LC_STAGE_ID	AND
					  OUTPUT_CATEGORY_ID	   = outputCategoryId			AND
					  DESTINATION_CODE         = sDESTINATION_CODE;
			END IF;


		END IF;
		
    END;

    PROCEDURE ManageWastewaterRow
    (
        sASSESSMENT_CODE VARCHAR2,
		dcASSESSMENT_LC_STAGE_ID NUMBER
    )
    IS        
        v_requiredWasterwaterRow NUMBER(1,0) := 0;
        v_wastewaterPresent NUMBER(1,0);
    BEGIN    
        WITH inputsWithWaterOutCats AS
        (
            -- Food and Non-food inputs - output categories
            SELECT (CASE WHEN DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
                      THEN 4
                    ELSE 6 END) AS OUTPUT_CATEGORY_ID
            FROM DXDIR_INPUT
              INNER JOIN DXDIR_INPUT_CATEGORY
              ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
            WHERE DXDIR_INPUT.INEDIBLE_PARTS < 1.0 
              AND DXDIR_INPUT.ASSESSMENT_CODE = sASSESSMENT_CODE AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID
              AND DXDIR_INPUT.MAT_PLANT = 'NONE'
              AND DXDIR_INPUT.MAT_CODE = (CASE WHEN DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
                                            THEN 'DIR_FOOD_WATER'
                                          ELSE 'DIR_NONFOOD_WATER' END)
            UNION
            -- Inedible parts inputs - output categories
            SELECT 5 AS OUTPUT_CATEGORY_ID
            FROM DXDIR_INPUT
              INNER JOIN DXDIR_INPUT_CATEGORY
              ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
            WHERE DXDIR_INPUT.INEDIBLE_PARTS > 0.0
              AND DXDIR_INPUT.ASSESSMENT_CODE = sASSESSMENT_CODE AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
              AND DXDIR_INPUT.MAT_PLANT = 'NONE'
              AND DXDIR_INPUT.MAT_CODE = 'DIR_FOOD_WATER'
        ),
        destOutCategoriesWithSewer AS
        (
            -- sewer input destinations - output categories
            SELECT DISTINCT INPT_DEST.OUTPUT_CATEGORY_ID 
            FROM DXDIR_INPUT_DESTINATION INPT_DEST
              INNER JOIN DXDIR_INPUT INPT
              ON INPT_DEST.INPUT_ID = INPT.ID
            WHERE INPT_DEST.DESTINATION_CODE = 'SEWER'
            AND INPT.ASSESSMENT_CODE = sASSESSMENT_CODE AND INPT.ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID
        )
        SELECT NVL(MAX(1), 0)
        INTO v_requiredWasterwaterRow
        FROM inputsWithWaterOutCats
        WHERE OUTPUT_CATEGORY_ID NOT IN (SELECT OUTPUT_CATEGORY_ID FROM destOutCategoriesWithSewer);

        SELECT (CASE WHEN COUNT(*) > 0 THEN 1 ELSE 0 END)
        INTO v_wastewaterPresent
        FROM DXDIR_OUTPUT
        WHERE ASSESSMENT_CODE = sASSESSMENT_CODE AND ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID
        AND OUTPUT_CATEGORY_ID = 7;

        IF (v_requiredWasterwaterRow = 1 AND v_wastewaterPresent = 0)
        THEN
            INSERT INTO DXDIR_OUTPUT
            (ID,
             ASSESSMENT_CODE,
             OUTPUT_CATEGORY_ID,
             ASSESSMENT_LC_STAGE_ID)
            VALUES 
            (DXDIR_OUTPUT_SEQ.NEXTVAL,
             sASSESSMENT_CODE,
             7,
             dcASSESSMENT_LC_STAGE_ID);
            
        ELSIF (v_requiredWasterwaterRow = 0 AND v_wastewaterPresent = 1)
        THEN
            DELETE FROM DXDIR_OUTPUT
            WHERE ASSESSMENT_CODE = sASSESSMENT_CODE AND ASSESSMENT_LC_STAGE_ID = dcASSESSMENT_LC_STAGE_ID AND OUTPUT_CATEGORY_ID = 7;
        END IF;
    END;

END;
/