IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT_DESTINATION$InsertRecord'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$InsertRecord]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$InsertRecord]
	@dcINPUT_ID BIGINT,
	@sDESTINATION_CODE VARCHAR(64),
	@dcOUTPUT_CATEGORY_ID BIGINT,
	@dcPERCENTAGE NUMERIC(4, 1)
AS
BEGIN
	INSERT [dbo].[DXDIR_INPUT_DESTINATION]
	(
		[INPUT_ID],
		[DESTINATION_CODE],
		[OUTPUT_CATEGORY_ID],
		[PERCENTAGE]
	)
	VALUES
	(
		@dcINPUT_ID,
		@sDESTINATION_CODE,
		@dcOUTPUT_CATEGORY_ID,
		@dcPERCENTAGE
	)
END
GO

IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT_DESTINATION$UpdateRecord'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$UpdateRecord]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$UpdateRecord]
	@dcINPUT_ID BIGINT,
	@sDESTINATION_CODE VARCHAR(64),
	@dcOUTPUT_CATEGORY_ID BIGINT,
	@dcPERCENTAGE NUMERIC(4, 1)
AS
BEGIN
	UPDATE [dbo].[DXDIR_INPUT_DESTINATION]
	SET 
		[PERCENTAGE] = @dcPERCENTAGE
	WHERE [INPUT_ID] = @dcINPUT_ID
	AND [DESTINATION_CODE] = @sDESTINATION_CODE
	AND [OUTPUT_CATEGORY_ID] = @dcOUTPUT_CATEGORY_ID
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT_DESTINATION$DeleteRecord'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$DeleteRecord]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$DeleteRecord]
	@dcINPUT_ID BIGINT
	,@sDESTINATION_CODE VARCHAR(64)
	,@dcOUTPUT_CATEGORY_ID BIGINT
AS
BEGIN
	DELETE FROM [dbo].[DXDIR_INPUT_DESTINATION]
	WHERE INPUT_ID = @dcINPUT_ID
	AND DESTINATION_CODE = @sDESTINATION_CODE
	AND OUTPUT_CATEGORY_ID =@dcOUTPUT_CATEGORY_ID
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT_DESTINATION$DeleteDestiantionByCodes'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$DeleteDestiantionByCodes]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$DeleteDestiantionByCodes]
	@dcINPUT_ID BIGINT,
	@sDESTINATION_CODES DX_Varchar2Table READONLY    
AS
BEGIN
	DELETE FROM [dbo].[DXDIR_INPUT_DESTINATION]
	WHERE INPUT_ID = @dcINPUT_ID
          AND DESTINATION_CODE IN (SELECT CONTENT FROM @sDESTINATION_CODES);
END
GO


IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT_DESTINATION$SelectFoodDestinations'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$SelectFoodDestinations]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$SelectFoodDestinations]
    @sASSESSMENT_CODE VARCHAR(32),
    @dcASSESSMENT_LC_STAGE_ID BIGINT
AS
BEGIN
	WITH cteINPUTS_UNION AS
    (
        SELECT DXDIR_INPUT.ID, 
                DXDIR_INPUT.ASSESSMENT_CODE, 
                4 AS OUTPUT_CATEGORY_ID,
                'FOOD' AS OUTPUT_CATEGORY_TITLE,
                DXDIR_INPUT.MAT_PLANT,
                DXDIR_INPUT.MAT_CODE,
                DXDIR_INPUT.PART_OF_PRODUCT_COPRODUCT,
                DXDIR_INPUT.CATEGORY_SORT_ORDER,
                DXDIR_INPUT.INPUT_SORT_ORDER
        FROM DXDIR_INPUT
            INNER JOIN DXDIR_INPUT_CATEGORY
            ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
        WHERE DXDIR_INPUT.INEDIBLE_PARTS < 1.0 AND DXDIR_INPUT.ASSESSMENT_CODE = @sASSESSMENT_CODE AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
        UNION ALL
        SELECT DXDIR_INPUT.ID,
                DXDIR_INPUT.ASSESSMENT_CODE,
                5 AS OUTPUT_CATEGORY_ID,
                'INEDIBLE_PARTS' AS OUTPUT_CATEGORY_TITLE,
                DXDIR_INPUT.MAT_PLANT,
                DXDIR_INPUT.MAT_CODE,
                0 AS PART_OF_PRODUCT_COPRODUCT,
                DXDIR_INPUT.CATEGORY_SORT_ORDER,
                DXDIR_INPUT.INPUT_SORT_ORDER
        FROM DXDIR_INPUT
            INNER JOIN DXDIR_INPUT_CATEGORY
            ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
        WHERE DXDIR_INPUT.INEDIBLE_PARTS > 0.0 AND DXDIR_INPUT.ASSESSMENT_CODE = @sASSESSMENT_CODE AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND DXDIR_INPUT_CATEGORY.TYPE = 'FOOD'
    ),
	cteINPUTS_W_DEST AS
	(
		SELECT cteINPUTS_UNION.ID as INPUT_ID,
				cteINPUTS_UNION.OUTPUT_CATEGORY_ID,
				cteINPUTS_UNION.OUTPUT_CATEGORY_TITLE,
				cteINPUTS_UNION.MAT_PLANT,
				cteINPUTS_UNION.MAT_CODE,
				cteINPUTS_UNION.PART_OF_PRODUCT_COPRODUCT,
				cteINPUTS_UNION.CATEGORY_SORT_ORDER,
                cteINPUTS_UNION.INPUT_SORT_ORDER,
				DXDIR_INPUT_DESTINATION.DESTINATION_CODE,
				DXDIR_INPUT_DESTINATION.PERCENTAGE
		FROM cteINPUTS_UNION
			LEFT JOIN DXDIR_INPUT_DESTINATION
			ON DXDIR_INPUT_DESTINATION.INPUT_ID = cteINPUTS_UNION.ID
			AND DXDIR_INPUT_DESTINATION.OUTPUT_CATEGORY_ID = cteINPUTS_UNION.OUTPUT_CATEGORY_ID
	)
	SELECT INPUT_ID,
		OUTPUT_CATEGORY_ID,
		OUTPUT_CATEGORY_TITLE,
		MAT_PLANT,
		MAT_CODE,
		PART_OF_PRODUCT_COPRODUCT,
		PRODUCT,
		PRODUCT_2,
		PRODUCT_3,
		COPRODUCT,
		COPRODUCT_2,
        FOOD_RESCUE,
		ANIMAL_FEED,
		BIOMASS_MATERIAL,
		CODIGESTION_ANAEROBIC,
		COMPOSTING,
		COMBUSTION,
		LAND_APP,
		LANDFILL,
		NOT_HARVESTED,
		REFUSE_DISCARD,
		SEWER,
		ENVIRONMENT_LOSS,
		OTHER
	FROM cteINPUTS_W_DEST
	PIVOT (
		MIN(PERCENTAGE)
		FOR DESTINATION_CODE
		-- below are all possible values from DXDIR_DESTINATION this can only be dynamic with PIVOT XML or DYNAMIC SQL
		IN (PRODUCT,
			PRODUCT_2,
			PRODUCT_3,
			COPRODUCT,
			COPRODUCT_2,
			FOOD_RESCUE,
			ANIMAL_FEED,
			BIOMASS_MATERIAL,
			CODIGESTION_ANAEROBIC,
			COMPOSTING,
			COMBUSTION,
			LAND_APP,
			LANDFILL,
			NOT_HARVESTED,
			REFUSE_DISCARD,
			SEWER,
			ENVIRONMENT_LOSS,
			OTHER
			)
	) AS pivot_table
    ORDER BY OUTPUT_CATEGORY_ID, CATEGORY_SORT_ORDER, INPUT_SORT_ORDER ASC;
END
GO

IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT_DESTINATION$SelectNonFoodDestinations'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$SelectNonFoodDestinations]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$SelectNonFoodDestinations]
    @sASSESSMENT_CODE VARCHAR(32),
    @dcASSESSMENT_LC_STAGE_ID BIGINT
AS
BEGIN
        WITH cteINPUTS_W_DEST AS
		(
			SELECT DXDIR_INPUT.ID as INPUT_ID,
					6 AS OUTPUT_CATEGORY_ID,
					'NON_FOOD' AS OUTPUT_CATEGORY_TITLE,
					DXDIR_INPUT.MAT_PLANT,
					DXDIR_INPUT.MAT_CODE,
					DXDIR_INPUT.PART_OF_PRODUCT_COPRODUCT,
					DXDIR_INPUT.CATEGORY_SORT_ORDER,
					DXDIR_INPUT.INPUT_SORT_ORDER,
					DXDIR_INPUT_DESTINATION.DESTINATION_CODE,
					DXDIR_INPUT_DESTINATION.PERCENTAGE
			FROM DXDIR_INPUT
				INNER JOIN DXDIR_INPUT_CATEGORY
				ON DXDIR_INPUT.INPUT_CATEGORY_ID = DXDIR_INPUT_CATEGORY.ID
				LEFT JOIN DXDIR_INPUT_DESTINATION
				ON DXDIR_INPUT_DESTINATION.INPUT_ID = DXDIR_INPUT.ID
				AND DXDIR_INPUT_DESTINATION.OUTPUT_CATEGORY_ID = 6
				WHERE DXDIR_INPUT.ASSESSMENT_CODE = @sASSESSMENT_CODE AND DXDIR_INPUT.ASSESSMENT_LC_STAGE_ID = @dcASSESSMENT_LC_STAGE_ID AND ISNULL(DXDIR_INPUT_CATEGORY.TYPE, '') <> 'FOOD'
		)
		SELECT INPUT_ID,
			OUTPUT_CATEGORY_ID,
			OUTPUT_CATEGORY_TITLE,
			MAT_PLANT,
			MAT_CODE,
			PART_OF_PRODUCT_COPRODUCT,
			PRODUCT,
			PRODUCT_2,
			PRODUCT_3,
			COPRODUCT,
			COPRODUCT_2,
			FOOD_RESCUE,
			RECYCLING,
			INCIN_WITH_EN_RECOVER,
			LANDFILL,
			SEWER,
			OTHER
		FROM cteINPUTS_W_DEST
		PIVOT (
			MIN(PERCENTAGE)
			FOR DESTINATION_CODE
			-- below are all possible values from DXDIR_DESTINATION this can only be dynamic with PIVOT XML or DYNAMIC SQL
			IN (PRODUCT,
				PRODUCT_2,
				PRODUCT_3,
				COPRODUCT,
				COPRODUCT_2,
				FOOD_RESCUE,
				RECYCLING,
				INCIN_WITH_EN_RECOVER,
				LANDFILL,
				SEWER,
				OTHER
				)
		) AS pivot_table
		ORDER BY OUTPUT_CATEGORY_ID, CATEGORY_SORT_ORDER, INPUT_SORT_ORDER ASC;
END
GO

IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT_DESTINATION$CheckValidationFillDestination'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$CheckValidationFillDestination]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$CheckValidationFillDestination]
	 @sASSESSMENT_CODE VARCHAR(32)
AS
BEGIN
	with cteOutputCategory AS 
            (
                SELECT DISTINCT DI.ASSESSMENT_LC_STAGE_ID, OUTPUT_CATEGORY_ID
                FROM DXDIR_INPUT DI INNER JOIN DXDIR_INPUT_DESTINATION DD
                    ON DI.ID = DD.INPUT_ID
                WHERE DI.ASSESSMENT_CODE = @sASSESSMENT_CODE
                GROUP BY DI.ASSESSMENT_LC_STAGE_ID, DI.ID, OUTPUT_CATEGORY_ID
                HAVING SUM(DD.PERCENTAGE) <> 100
            )
            SELECT ASSESSMENT_LC_STAGE_ID AS LCSTAGE , TITLE AS TITLE, 
                (CASE
                    WHEN SUM(OUTPUT_CATEGORY_ID) = 10 THEN 3
                    WHEN SUM(OUTPUT_CATEGORY_ID) = 6  THEN 2
                    WHEN SUM(OUTPUT_CATEGORY_ID) = 4  THEN 1
                ELSE 0 
                END) AS INVALID
            FROM cteOutputCategory cteO INNER JOIN DXDIR_ASSESSMENT_LC_STAGE DALC
                ON cteO.ASSESSMENT_LC_STAGE_ID = DALC.ID
            WHERE DALC.ASSESSMENT_CODE = @sASSESSMENT_CODE
            GROUP BY ASSESSMENT_LC_STAGE_ID, TITLE;
END
GO

IF EXISTS ( SELECT *
			FROM sys.objects so
			JOIN sys.schemas sc ON so.schema_id = sc.schema_id
			WHERE so.NAME = N'DXDIR_PK_INPUT_DESTINATION$DeleteByDestination'
			  AND so.type IN (N'P') AND sc.NAME = N'dbo' )
	DROP PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$DeleteByDestination]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[DXDIR_PK_INPUT_DESTINATION$DeleteByDestination]
	@sASSESSMENT_CODE VARCHAR(64),
	@sDESTINATION_CODE VARCHAR(64)
AS
BEGIN
	DELETE FROM [dbo].[DXDIR_INPUT_DESTINATION]
	WHERE INPUT_ID in ( SELECT ID FROM DXDIR_INPUT WHERE DXDIR_INPUT.ASSESSMENT_CODE = @sASSESSMENT_CODE )
		AND DESTINATION_CODE = @sDESTINATION_CODE;
END
GO